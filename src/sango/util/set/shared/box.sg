###########################################################################
# MIT License                                                             #
# Copyright (c) 2024 AKIYAMA Isao                                         #
#                                                                         #
# Permission is hereby granted, free of charge, to any person obtaining   #
# a copy of this software and associated documentation files (the         #
# "Software"), to deal in the Software without restriction, including     #
# without limitation the rights to use, copy, modify, merge, publish,     #
# distribute, sublicense, and/or sell copies of the Software, and to      #
# permit persons to whom the Software is furnished to do so, subject to   #
# the following conditions:                                               #
#                                                                         #
# The above copyright notice and this permission notice shall be          #
# included in all copies or substantial portions of the Software.         #
#                                                                         #
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,         #
# EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF      #
# MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.  #
# IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY    #
# CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,    #
# TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE       #
# SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.                  #
###########################################################################
module @beta "sango.util.set.shared.box" ;;

import "sango.actor.util" -> actu ;;
import "sango.entity.box" -> box ;;
import "sango.list" -> list ;;
import "sango.util.set" -> set ;;
import "sango.util.set.shared" -> shset ;;

eval <*A! set.set> *Set create_box @public -> <A shset.shared_set_h> {
  Set none$ box.create_box = *Box,
  Box actu.create_box_monitor = *Mon,

  extract_f: \\ <A iter> { Mon extract }
  empty?_f: \\ <bool> { Mon empty? }
  size_f: \\ <int> { Mon size }
  get_f: \ <A> *Elem -> <A maybe> { Mon Elem get }
  contain?_f_: (\ <A> *Elem -> <bool> { Mon Elem contain? } value$)
  put_f: \ <A> *Elem -> <A maybe> { Mon Elem put }
  put_new_f_: (\ <A> *Elem -> <bool> { Mon Elem put_new } value$)
  add_f_: (\ <A> *Elem -> <void> { Mon Elem add } value$)
  remove_f: \ <A> *Elem -> <A maybe> { Mon Elem remove }
  delete_f_: (\ <A> *Elem -> <void> { Mon Elem delete } value$)
  take_f: \\ <A maybe> { Mon take }
  clear_f: \\ <void> { Mon clear }
  shset.shared_set_h$
} ;;

eval <<*A! set.set> actu.box_monitor_h> *Mon extract -> <A iter> {
  Mon
    \ <<A set.set> box.box_h> *Box -> <A set.set> {
      Box box.read
    }
    actu.run = *Z,
  Z set.iter
} ;;

eval <<*A! set.set> actu.box_monitor_h> *Mon empty? -> <bool> {
  Mon
    \ <<A set.set> box.box_h> *Box -> <A set.set> {
      Box box.read
    } actu.run = *Z,
  Z set.empty?
} ;;

eval <<*A! set.set> actu.box_monitor_h> *Mon size -> <int> {
  Mon
    \ <<A set.set> box.box_h> *Box -> <A set.set> {
      Box box.read
    } actu.run = *Z,
  Z set.size
} ;;

eval <<*A! set.set> actu.box_monitor_h> *Mon <A> *Elem get -> <A maybe> {
  Mon
    \ <<A set.set> box.box_h> *Box -> <A set.set> {
      Box box.read
    } actu.run = *Z,
  Z Elem set.get
} ;;

eval <<*A! set.set> actu.box_monitor_h> *Mon <A> *Elem contain? -> <bool> {
  Mon
    \ <<A set.set> box.box_h> *Box -> <A set.set> {
      Box box.read
    } actu.run = *Z,
  Z Elem set.contain?
} ;;

eval <<*A! set.set> actu.box_monitor_h> *Mon <A> *Elem put -> <A maybe> {
  Mon
    \ <<A set.set> box.box_h> *Box -> <A maybe> {
      Box box.read = *Z,
      Z Elem set.put = (| *Z', *Replaced_ |),
      Box Z' box.write,
      Replaced_
    } actu.run
} ;;

eval <<*A! set.set> actu.box_monitor_h> *Mon <A> *Elem put_new -> <bool> {
  Mon
    \ <<A set.set> box.box_h> *Box -> <bool> {
      Box box.read = *Z,
      Z Elem set.put_new = (| *Z', *Succeeded? |),
      Box Z' box.write,
      Succeeded?
    } actu.run
} ;;

eval <<*A! set.set> actu.box_monitor_h> *Mon <A> *Elem add -> <void> {
  Mon
    \ <<A set.set> box.box_h> *Box -> <void> {
      Box box.read = *Z,
      Z Elem set.add = *Z',
      Box Z' box.write >> ignore
    } actu.run
} ;;

eval <<*A! set.set> actu.box_monitor_h> *Mon <A> *Elem remove -> <A maybe> {
  Mon
    \ <<A set.set> box.box_h> *Box -> <A maybe> {
      Box box.read = *Z,
      Z Elem set.remove = (| *Z', *Removed_ |),
      Box Z' box.write,
      Removed_
    } actu.run
} ;;

eval <<*A! set.set> actu.box_monitor_h> *Mon <A> *Elem delete -> <void> {
  Mon
    \ <<A set.set> box.box_h> *Box -> <void> {
      Box box.read = *Z,
      Z Elem set.delete = *Z',
      Box Z' box.write >> ignore
    } actu.run
} ;;

eval <<*A! set.set> actu.box_monitor_h> *Mon take -> <A maybe> {
  Mon
    \ <<A set.set> box.box_h> *Box -> <A maybe> {
      Box box.read = *Z,
      Z set.take >> case {
      ; (| *Elem, *Z' |) value$ -> Box Z' box.write, Elem value$
      ; ** -> none$
      }
    } actu.run
} ;;

eval <<*A! set.set> actu.box_monitor_h> *Mon clear -> <void> {
  Mon
    \ <<A set.set> box.box_h> *Box -> <void> {
      Box box.read = *Z,
      Z set.clear = *Z',
      Box Z' box.write >> ignore
    } actu.run
} ;;
