###########################################################################
# MIT License                                                             #
# Copyright (c) 2024 AKIYAMA Isao                                         #
#                                                                         #
# Permission is hereby granted, free of charge, to any person obtaining   #
# a copy of this software and associated documentation files (the         #
# "Software"), to deal in the Software without restriction, including     #
# without limitation the rights to use, copy, modify, merge, publish,     #
# distribute, sublicense, and/or sell copies of the Software, and to      #
# permit persons to whom the Software is furnished to do so, subject to   #
# the following conditions:                                               #
#                                                                         #
# The above copyright notice and this permission notice shall be          #
# included in all copies or substantial portions of the Software.         #
#                                                                         #
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,         #
# EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF      #
# MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.  #
# IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY    #
# CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,    #
# TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE       #
# SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.                  #
###########################################################################
module @alpha "sango.util.set.shared.box" ;;

import "sango.actor.util" -> actu ;;
import "sango.entity.box" -> box ;;
import "sango.list" -> list ;;
import "sango.util.set" -> set ;;
import "sango.util.set.shared" -> shset ;;

eval <*T![ *A! set.a'set ]> *FSet create_box @public -> <A shset.shared_set_h> {
  FSet set._feature_a'set = *Set_fo,
  Set_fo none$ box.create_box = *Box,
  Box actu.create_box_monitor = *Mon,

  extract_f: \\ <A iter> { Mon extract }
  empty?_f: \\ <bool> { Mon empty? }
  size_f: \\ <int> { Mon size }
  get_f: \ <A> *Elem -> <A maybe> { Mon Elem get }
  contain?_f_: (\ <A> *Elem -> <bool> { Mon Elem contain? } value$)
  put_f: \ <A> *Elem -> <A maybe> { Mon Elem put }
  put_new_f_: (\ <A> *Elem -> <bool> { Mon Elem put_new } value$)
  add_f_: (\ <A> *Elem -> <void> { Mon Elem add } value$)
  remove_f: \ <A> *Elem -> <A maybe> { Mon Elem remove }
  delete_f_: (\ <A> *Elem -> <void> { Mon Elem delete } value$)
  take_f: \\ <A maybe> { Mon take }
  clear_f: \\ <void> { Mon clear }
  shset.shared_set_h$
} ;;

eval <<*T! *A! set.set_fo> actu.box_monitor_h> *Mon extract -> <A iter> {
  Mon
    \ <<T A set.set_fo> box.box_h> *Box -> <T A set.set_fo> {
      Box box.read
    }
    actu.run = *Fo,
  \\ <A iter_res> { Fo fo_iter_next } iter$
} ;;

eval <*T! *A! set.set_fo> *Fo fo_iter_next -> <A iter_res> {
  Fo set.fo_take >> case {
  ; (| *E, *Fo' |) value$ -> (| E, \\ <A iter_res> { Fo' fo_iter_next } iter$ |) value$
  ; ** -> none$
  }
} ;;

eval <<*T! *A! set.set_fo> actu.box_monitor_h> *Mon empty? -> <bool> {
  Mon
    \ <<T A set.set_fo> box.box_h> *Box -> <T A set.set_fo> {
      Box box.read
    } actu.run = *Fo,
  Fo set.fo_empty?
} ;;

eval <<*T! *A! set.set_fo> actu.box_monitor_h> *Mon size -> <int> {
  Mon
    \ <<T A set.set_fo> box.box_h> *Box -> <T A set.set_fo> {
      Box box.read
    } actu.run = *Fo,
  Fo set.fo_size
} ;;

eval <<*T! *A! set.set_fo> actu.box_monitor_h> *Mon <A> *Elem get -> <A maybe> {
  Mon
    \ <<T A set.set_fo> box.box_h> *Box -> <T A set.set_fo> {
      Box box.read
    } actu.run = *Fo,
  Fo Elem set.fo_get
} ;;

eval <<*T! *A! set.set_fo> actu.box_monitor_h> *Mon <A> *Elem contain? -> <bool> {
  Mon
    \ <<T A set.set_fo> box.box_h> *Box -> <T A set.set_fo> {
      Box box.read
    } actu.run = *Fo,
  Fo Elem set.fo_contain?
} ;;

eval <<*T! *A! set.set_fo> actu.box_monitor_h> *Mon <A> *Elem put -> <A maybe> {
  Mon
    \ <<T A set.set_fo> box.box_h> *Box -> <A maybe> {
      Box box.read = *Fo,
      Fo Elem set.fo_put = (| *Fo', *Replaced_ |),
      Box Fo' box.write,
      Replaced_
  } actu.run
} ;;

eval <<*T! *A! set.set_fo> actu.box_monitor_h> *Mon <A> *Elem put_new -> <bool> {
  Mon
    \ <<T A set.set_fo> box.box_h> *Box -> <bool> {
      Box box.read = *Fo,
      Fo Elem set.fo_put_new = (| *Fo', *Succeeded? |),
      Box Fo' box.write,
      Succeeded?
  } actu.run
} ;;

eval <<*T! *A! set.set_fo> actu.box_monitor_h> *Mon <A> *Elem add -> <void> {
  Mon
    \ <<T A set.set_fo> box.box_h> *Box -> <void> {
      Box box.read = *Fo,
      Fo Elem set.fo_add = *Fo',
      Box Fo' box.write >> ignore
  } actu.run
} ;;

eval <<*T! *A! set.set_fo> actu.box_monitor_h> *Mon <A> *Elem remove -> <A maybe> {
  Mon
    \ <<T A set.set_fo> box.box_h> *Box -> <A maybe> {
      Box box.read = *Fo,
      Fo Elem set.fo_remove = (| *Fo', *Removed_ |),
      Box Fo' box.write,
      Removed_
  } actu.run
} ;;

eval <<*T! *A! set.set_fo> actu.box_monitor_h> *Mon <A> *Elem delete -> <void> {
  Mon
    \ <<T A set.set_fo> box.box_h> *Box -> <void> {
      Box box.read = *Fo,
      Fo Elem set.fo_delete = *Fo',
      Box Fo' box.write >> ignore
  } actu.run
} ;;

eval <<*T! *A! set.set_fo> actu.box_monitor_h> *Mon take -> <A maybe> {
  Mon
    \ <<T A set.set_fo> box.box_h> *Box -> <A maybe> {
      Box box.read = *Fo,
      Fo set.fo_take >> case {
      ; (| *Elem, *Fo' |) value$ -> Box Fo' box.write, Elem value$
      ; ** -> none$
      }
  } actu.run
} ;;

eval <<*T! *A! set.set_fo> actu.box_monitor_h> *Mon clear -> <void> {
  Mon
    \ <<T A set.set_fo> box.box_h> *Box -> <void> {
      Box box.read = *Fo,
      Fo set.fo_clear = *Fo',
      Box Fo' box.write >> ignore
  } actu.run
} ;;
