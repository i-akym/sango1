###########################################################################
# MIT License                                                             #
# Copyright (c) 2018 Isao Akiyama                                         #
#                                                                         #
# Permission is hereby granted, free of charge, to any person obtaining   #
# a copy of this software and associated documentation files (the         #
# "Software"), to deal in the Software without restriction, including     #
# without limitation the rights to use, copy, modify, merge, publish,     #
# distribute, sublicense, and/or sell copies of the Software, and to      #
# permit persons to whom the Software is furnished to do so, subject to   #
# the following conditions:                                               #
#                                                                         #
# The above copyright notice and this permission notice shall be          #
# included in all copies or substantial portions of the Software.         #
#                                                                         #
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,         #
# EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF      #
# MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.  #
# IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY    #
# CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,    #
# TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE       #
# SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.                  #
###########################################################################
module "sango.util.queue" ;;

import "sango.list" -> list ;;
import "sango.util.iter" -> iter ;;
import "sango.util.queue.feature" -> feat ;;

### function set

data <*D *A queue_fs> @public :=
  empty?_f: <D bool fun>
  length_f: <D int fun>
  enqueue_f: <D A D fun>
  head_f: <D <A maybe> fun>
  dequeue_f: <D <<A D tuple> maybe> fun>
  clear_f: <D D fun>
  queue_fs$ ;;

eval <*D *A queue_fs> *Fs queue_fs_to_col_fs | to_col_fs @public -> <D A col_fs> {
  Fs = empty?_f: *Empty_f length_f: *Length_f *** queue_fs$,
  empty?_f_: (Empty_f value$)
  size_f: Length_f
  iter_f: \ <D> *Z -> <A iter> { Z Fs do_iter }
  col_fs$
} ;;


### combined object

data <*A queue> @opaque :=
  empty?_f: <bool fun>
  length_f: <int fun>
  enqueue_f: <A <A queue> fun>
  head_f: <<A maybe> fun>
  dequeue_f: <<<A <A queue> tuple> maybe> fun>
  clear_f: <<A queue> fun>
  queue$
++ a'queue_impl -> [ A feat.a'queue ] ;;

eval <*D> *Z <D *A queue_fs> *Fs combine @public -> <A queue> {
  empty?_f:
    \\ <bool> { Z Fs do_empty? }
  length_f:
    \\ <int> { Z Fs do_length }
  enqueue_f:
    \ <A> *X -> <A queue> {
      Z Fs X do_enqueue = *Z',
      Z' Fs combine
    }
  head_f:
    \\ <A maybe> { Z Fs do_head }
  dequeue_f:
    \\ <<A <A queue> tuple> maybe> {
      Z Fs do_dequeue >> case {
      ; (| *E, *Z' |) value$ -> (| E, Z' Fs combine |) value$
      ; ** -> none$
      }
    }
  clear_f:
    \\ <A queue> {
      Z Fs do_clear = *Z',
      Z' Fs combine
    }
  queue$
} ;;

eval a'queue_impl -> <<*A queue> A queue_fs> {
  empty?_f: ^empty?
  length_f: ^length
  enqueue_f: ^enqueue
  head_f: ^head
  dequeue_f: ^dequeue
  clear_f: ^clear
  queue_fs$
} ;;


### implementation of common-behavior object

eval <*A queue> *Queue empty? @public -> <bool> {
  &(Queue empty?_f)
} ;;

eval <*A queue> *Queue length @public -> <int> {
  &(Queue length_f)
} ;;

eval <*A queue> *Queue <A> *Elem enqueue @public -> <A queue> {
  Elem &(Queue enqueue_f)
} ;;

eval <*A queue> *Queue head @public -> <A maybe> {
  &(Queue head_f)
} ;;

eval <*A queue> *Queue dequeue @public -> <<A <A queue> tuple> maybe> {
  &(Queue dequeue_f)
} ;;

eval <*A queue> *Queue clear @public -> <A queue> {
  &(Queue clear_f)
} ;;

eval <*A queue> *Queue iter @public -> <A iter> {
  \\ <A iter_res> { Queue iter_next } iter$
} ;;

eval <*A queue> *Z iter_next -> <A iter_res> {
  Z dequeue >> case {
  ; (| *E, *Z' |) value$ ->
    (| E, \\ <A iter_res> { Z' iter_next } iter$ |) value$
  ; ** -> none$
  }
} ;;

eval <*A queue> *Queue <A list> *Elems enqueue_list @public -> <A queue> {
  Queue (Elems list.iter) enqueue_iter
} ;;

eval <*A queue> *Queue <A iter> *Iter enqueue_iter @public -> <A queue> {
  Queue
  Iter
  \ <A queue> *Y <A> *E -> <A queue> { Y E enqueue }
  iter.fold
} ;;


### subroutines

eval <*D> *Z <D *A queue_fs> *Fs do_empty? @public -> <bool> {
  Z &(Fs empty?_f)
} ;;

eval <*D> *Z <D *A queue_fs> *Fs do_length @public -> <int> {
  Z &(Fs length_f)
} ;;

eval <*D> *Z <D *A queue_fs> *Fs <A> *Elem do_enqueue @public -> <D> {
  Z Elem &(Fs enqueue_f)
} ;;

eval <*D> *Z <D *A queue_fs> *Fs do_head @public -> <A maybe> {
  Z &(Fs head_f)
} ;;

eval <*D> *Z <D *A queue_fs> *Fs do_dequeue @public -> <<A D tuple> maybe> {
  Z &(Fs dequeue_f)
} ;;

eval <*D> *Z <D *A queue_fs> *Fs do_clear @public -> <D> {
  Z &(Fs clear_f)
} ;;

eval <*D> *Z <D *A queue_fs> *Fs <A list> *Elems do_enqueue_list | enqueue_list @public -> <D> {
  Z Fs (Elems list.iter) do_enqueue_iter
} ;;

eval <*D> *Z <D *A queue_fs> *Fs <A iter> *Iter do_enqueue_iter | enqueue_iter @public -> <D> {
  Z
  Iter
  \ <D> *Y <A> *E -> <D> { Y Fs E do_enqueue }
  iter.fold
} ;;

eval <*D> *Z <D *A queue_fs> *Fs do_iter | iter @public -> <A iter> {
  \\ <A iter_res> { Z Fs do_iter_next } iter$
} ;;

eval <*D> *Z <D *A queue_fs> *Fs do_iter_next -> <A iter_res> {
  Z Fs do_dequeue >> case {
  ; (| *Elem, *Z' |) value$ ->
    (| Elem, \\ <A iter_res> { Z' Fs do_iter_next } iter$ |) value$
  ; ** -> none$
  }
} ;;
