###########################################################################
# MIT License                                                             #
# Copyright (c) 2018 Isao Akiyama                                         #
#                                                                         #
# Permission is hereby granted, free of charge, to any person obtaining   #
# a copy of this software and associated documentation files (the         #
# "Software"), to deal in the Software without restriction, including     #
# without limitation the rights to use, copy, modify, merge, publish,     #
# distribute, sublicense, and/or sell copies of the Software, and to      #
# permit persons to whom the Software is furnished to do so, subject to   #
# the following conditions:                                               #
#                                                                         #
# The above copyright notice and this permission notice shall be          #
# included in all copies or substantial portions of the Software.         #
#                                                                         #
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,         #
# EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF      #
# MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.  #
# IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY    #
# CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,    #
# TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE       #
# SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.                  #
###########################################################################
module "sango.util.map" ;;

import "sango.list" -> list ;;
import "sango.tuple" -> tuple ;;
import "sango.util.iter" -> iter ;;

### definition of function set

data <*D *A *B map_fs> @public :=
  empty?_f: <D bool fun>
  size_f: <D int fun>
  get_f: <D A <<A B tuple> maybe> fun>
  assoc_f_: <<D A <B maybe> fun> maybe>
  contain_key?_f_: <<D A bool fun> maybe>
  put_f: <D A B <D <<A B tuple> maybe> tuple> fun>
  put_new_f_: <<D A B <D bool tuple> fun> maybe>
  add_f_: <<D A B D fun> maybe>
  remove_f: <D A <D <<A B tuple> maybe> tuple> fun>
  delete_f_: <<D A D fun> maybe>
  take_f: <D <<<A B tuple> D tuple> maybe> fun>
  clear_f: <D D fun>
  map_fs$ ;;


### definition of feature

feature <*T> [ *A *B a'map ] @public -> <T A B map_fs> ;;


### definition of common-behavior object

alias type <*A *B d> @public := <A B _2+> ;;

data <*A *B map> @public :=
  d: <A B d>
  fs: <<A B d> A B map_fs>
  map$
++ a'map_impl -> [ A B a'map ] ;;

eval a'map_impl -> <<*A *B map> A B map_fs> {
  empty?_f: ^c_empty?
  size_f: ^c_size
  get_f: ^c_get
  assoc_f_: (^c_assoc value$)
  contain_key?_f_: (^c_contain_key? value$)
  put_f: ^c_put
  put_new_f_: (^c_put_new value$)
  add_f_: (^c_add value$)
  remove_f: ^c_remove
  delete_f_: (^c_delete value$)
  take_f: ^c_take
  clear_f: ^c_clear
  map_fs$
} ;;


### implementation of common-behavior object

eval <*A *B map> *Map c_empty? | empty? @public -> <bool> {
  Map = d: *D fs: *Fs map$,
  D Fs do_empty?
} ;;

eval <*A *B map> *Map c_size | size @public -> <int> {
  Map = d: *D fs: *Fs map$,
  D Fs do_size
} ;;

eval <*A *B map> *Map <A> *Key c_get | get @public -> <<A B tuple> maybe> {
  Map = d: *D fs: *Fs map$,
  D Fs Key do_get
} ;;

eval <*A *B map> *Map <A> *Key c_assoc | assoc @public -> <B maybe> {
  Map = d: *D fs: *Fs map$,
  D Fs Key do_assoc
} ;;

eval <*A *B map> *Map <A> *Key c_contain_key? | contain_key? @public -> <bool> {
  Map = d: *D fs: *Fs map$,
  D Fs Key do_contain_key?
} ;;

eval <*A *B map> *Map <A> *Key <B> *Value c_put | put @public -> <<A B map> <<A B tuple> maybe> tuple> {
  Map = d: *D fs: *Fs map$,
  D Fs Key Value do_put = (| *D', *Replaced_kv_ |),
  (| d: D' fs: Fs map$, Replaced_kv_ |)
} ;;

eval <*A *B map> *Map <A> *Key <B> *Value c_put_new | put_new @public -> <<A B map> bool tuple> {
  Map = d: *D fs: *Fs map$,
  D Fs Key Value do_put_new = (| *D', *Succeeded? |),
  (| d: D' fs: Fs map$, Succeeded? |)
} ;;

eval <*A *B map> *Map <A> *Key <B> *Value c_add | add @public -> <A B map> {
  Map = d: *D fs: *Fs map$,
  D Fs Key Value do_add = *D',
  D' fs: Fs map$
} ;;

eval <*A *B map> *Map <A> *Key c_remove | remove @public -> <<A B map> <<A B tuple> maybe> tuple> {
  Map = d: *D fs: *Fs map$,
  D Fs Key do_remove = (| *D', *Removed_kv_ |),
  (| D' fs: Fs map$, Removed_kv_ |)
} ;;

eval <*A *B map> *Map <A> *Key c_delete | delete @public -> <A B map> {
  Map = d: *D fs: *Fs map$,
  D Fs Key do_delete = *D',
  D' fs: Fs map$
} ;;

eval <*A *B map> *Map c_take | take @public -> <<<A B tuple> <A B map> tuple> maybe> {
  Map = d: *D fs: *Fs map$,
  D Fs do_take >> case {
  ; (| *KV, *D' |) value$ -> (| KV, D' Fs map$ |) value$
  ; ** -> none$
  }
} ;;

eval <*A *B map> *Map c_clear | clear @public -> <A B map> {
  Map = d: *D fs: *Fs map$,
  D Fs do_clear = *D',
  D' Fs map$
} ;;

eval <*A *B map> *Map <<A B tuple> list> *KVs c_add_list | add_list @public -> <A B map> {
  Map (KVs list.iter) c_add_iter
} ;;

eval <*A *B map> *Map <<A B tuple> iter> *Iter c_add_iter | add_iter @public -> <A B map> {
  Map = d: *D fs: *Fs map$,
  D Fs Iter do_add_iter = *D',
  D' Fs map$
} ;;

eval <*A *B map> *Map <A list> *Ks c_delete_list | delete_list @public -> <A B map> {
  Map (Ks list.iter) c_delete_iter
} ;;

eval <*A *B map> *Map <A iter> *Iter c_delete_iter | delete_iter @public -> <A B map> {
  Map = d: *D fs: *Fs map$,
  D Fs Iter do_delete_iter = *D',
  D' Fs map$
} ;;

eval <*A *B map> *Map c_iter | iter @public -> <<A B tuple> iter> {
  Map = d: *D fs: *Fs map$,
  D Fs do_iter
} ;;


### framework of faeture

eval <*T[ *A *B a'map ]> *FMap f_empty? | empty? @public -> <bool> {
  FMap _feature_a'map = *Map_fs,
  FMap Map_fs do_empty?
} ;;

eval <*T[ *A *B a'map ]> *FMap f_size | size @public -> <int> {
  FMap _feature_a'map = *Map_fs,
  FMap Map_fs do_size
} ;;

eval <*T[ *A *B a'map ]> *FMap <A> *Key f_get | get @public -> <<A B tuple> maybe> {
  FMap _feature_a'map = *Map_fs,
  FMap Map_fs Key do_get
} ;;

eval <*T[ *A *B a'map ]> *FMap <A> *Key f_assoc | assoc @public -> <B maybe> {
  FMap _feature_a'map = *Map_fs,
  FMap Map_fs Key do_assoc
} ;;

eval <*T[ *A *B a'map ]> *FMap <A> *Key f_contain_key? | contain_key? @public -> <bool> {
  FMap _feature_a'map = *Map_fs,
  FMap Map_fs Key do_contain_key?
} ;;

eval <*T[ *A *B a'map ]> *FMap <A> *Key <B> *Value f_put | put @public -> <T <<A B tuple> maybe> tuple> {
  FMap _feature_a'map = *Map_fs,
  FMap Map_fs Key Value do_put
} ;;

eval <*T[ *A *B a'map ]> *FMap <A> *Key <B> *Value f_put_new | put_new @public -> <T bool tuple> {
  FMap _feature_a'map = *Map_fs,
  FMap Map_fs Key Value do_put_new
} ;;

eval <*T[ *A *B a'map ]> *FMap <A> *Key <B> *Value f_add | add @public -> <T> {
  FMap _feature_a'map = *Map_fs,
  FMap Map_fs Key Value do_add
} ;;

eval <*T[ *A *B a'map ]> *FMap <A> *Key f_remove | remove @public -> <T <<A B tuple> maybe> tuple> {
  FMap _feature_a'map = *Map_fs,
  FMap Map_fs Key do_remove
} ;;

eval <*T[ *A *B a'map ]> *FMap <A> *Key f_delete | delete @public -> <T> {
  FMap _feature_a'map = *Map_fs,
  FMap Map_fs Key do_delete
} ;;

eval <*T[ *A *B a'map ]> *FMap f_take | take @public -> <<<A B tuple> T tuple> maybe> {
  FMap _feature_a'map = *Map_fs,
  FMap Map_fs do_take
} ;;

eval <*T[ *A *B a'map ]> *FMap f_clear | clear @public -> <T> {
  FMap _feature_a'map = *Map_fs,
  FMap Map_fs do_clear
} ;;

eval <*T[ *A *B a'map ]> *FMap <<A B tuple> list> *KVs f_add_list | add_list @public -> <T> {
  FMap (KVs list.iter) f_add_iter
} ;;

eval <*T[ *A *B a'map ]> *FMap <<A B tuple> iter> *Iter f_add_iter | add_iter @public -> <T> {
  FMap _feature_a'map = *Map_fs,
  FMap Map_fs Iter do_add_iter
} ;;

eval <*T[ *A *B a'map ]> *FMap <A list> *Ks f_delete_list | delete_list @public -> <T> {
  FMap (Ks list.iter) f_delete_iter
} ;;

eval <*T[ *A *B a'map ]> *FMap <A iter> *Iter f_delete_iter | delete_iter @public -> <T> {
  FMap _feature_a'map = *Map_fs,
  FMap Map_fs Iter do_delete_iter
} ;;

eval <*T[ *A *B a'map ]> *FMap f_iter | iter @public -> <<A B tuple> iter> {
  FMap _feature_a'map = *Map_fs,
  FMap Map_fs do_iter
} ;;


### subroutines

eval <*D *A *B map_fs> *Fs map_fs_to_col_fs | to_col_fs @public -> <D <A B tuple> col_fs> {
  Fs = empty?_f: *Empty_f size_f: *Size_f *** map_fs$,
  empty?_f_: (Empty_f value$)
  size_f: Size_f
  iter_f: \ <D> *Z -> <<A B tuple> iter> { Z Fs do_iter }
  col_fs$
} ;;

eval <*D *A *B map_fs> *Fs map_fs_assoc_f | assoc_f @public -> <D A <B maybe> fun> {
  Fs assoc_f_ >> case {
  ; *F value$ -> F
  ; none$ ->
    \ <D> *M <A> *Key -> <B maybe> {
      (M Key &(Fs get_f)) ^tuple.pair_second apply_to_something
    }
  }
} ;;

eval <*D *A *B map_fs> *Fs map_fs_contain_key?_f | contain_key?_f @public -> <D A bool fun> {
  Fs contain_key?_f_ >> case {
  ; *F value$ -> F
  ; none$ ->
    \ <D> *M <A> *Key -> <bool> {
      M Key &(Fs get_f) >> something?
    }
  }
} ;;

eval <*D *A *B map_fs> *Fs map_fs_put_new_f | put_new_f @public -> <D A B <D bool tuple> fun> {
  Fs put_new_f_ >> case {
  ; *F value$ -> F
  ; none$ ->
    \ <D> *M <A> *Key <B> *Value -> <D bool tuple> {
      M Key &(Fs contain_key?_f) >> case {
      ; true$ -> (| M, false$ |)
      ; ** -> (| M Key Value &(Fs add_f), true$ |)
      }
    }
  }
} ;;

eval <*D *A *B map_fs> *Fs map_fs_add_f | add_f @public -> <D A B D fun> {
  Fs add_f_ >> case {
  ; *F value$ -> F
  ; none$ ->
    \ <D> *M <A> *Key <B> *Value -> <D> {
      M Key Value &(Fs put_f) >> tuple.pair_first
    }
  }
} ;;

eval <*D *A *B map_fs> *Fs map_fs_delete_f | delete_f @public -> <D A D fun> {
  Fs delete_f_ >> case {
  ; *F value$ -> F
  ; none$ ->
    \ <D> *M <A> *Key -> <D> {
      M Key &(Fs remove_f) >> tuple.pair_first
    }
  }
} ;;

eval <*D> *Z <D *A *B map_fs> *Fs do_empty? @public -> <bool> {
  Z &(Fs empty?_f)
} ;;

eval <*D> *Z <D *A *B map_fs> *Fs do_size @public -> <int> {
  Z &(Fs size_f)
} ;;

eval <*D> *Z <D *A *B map_fs> *Fs <A> *Key do_get @public -> <<A B tuple> maybe> {
  Z Key &(Fs get_f)
} ;;

eval <*D> *Z <D *A *B map_fs> *Fs <A> *Key do_assoc @public -> <B maybe> {
  Z Key &(Fs assoc_f)
} ;;

eval <*D> *Z <D *A *B map_fs> *Fs <A> *Key do_contain_key? @public -> <bool> {
  Z Key &(Fs contain_key?_f)
} ;;

eval <*D> *Z <D *A *B map_fs> *Fs <A> *Key <B> *Value do_put @public -> <D <<A B tuple> maybe> tuple>  {
  Z Key Value &(Fs put_f)
} ;;

eval <*D> *Z <D *A *B map_fs> *Fs <A> *Key <B> *Value do_put_new @public -> <D bool tuple>  {
  Z Key Value &(Fs put_new_f)
} ;;

eval <*D> *Z <D *A *B map_fs> *Fs <A> *Key <B> *Value do_add @public -> <D>  {
  Z Key Value &(Fs add_f)
} ;;

eval <*D> *Z <D *A *B map_fs> *Fs <A> *Key do_remove @public -> <D <<A B tuple> maybe> tuple>  {
  Z Key &(Fs remove_f)
} ;;

eval <*D> *Z <D *A *B map_fs> *Fs <A> *Key do_delete @public -> <D>  {
  Z Key &(Fs delete_f)
} ;;

eval <*D> *Z <D *A *B map_fs> *Fs do_take @public -> <<<A B tuple> D tuple> maybe> {
  Z &(Fs take_f)
} ;;

eval <*D> *Z <D *A *B map_fs> *Fs do_clear @public -> <D> {
  Z &(Fs clear_f)
} ;;

eval <*D> *Z <D *A *B map_fs> *Fs <<A B tuple> list> *KVs do_add_list | add_list @public -> <D> {
  Z Fs (KVs list.iter) do_add_iter
} ;;

eval <*D> *Z <D *A *B map_fs> *Fs <<A B tuple> iter> *Iter do_add_iter | add_iter @public -> <D> {
  Z Iter &\ <D> *Y <<A B tuple> iter> *I -> <D> {
    I next >> case {
    ; (| (| *K, *V |), *I' |) value$ -> (Y Fs K V do_add) I' &&
    ; ** -> Y
    }
  }
} ;;

eval <*D> *Z <D *A *B map_fs> *Fs <A list> *Ks do_delete_list | delete_list @public -> <D> {
  Z Fs (Ks list.iter) do_delete_iter
} ;;

eval <*D> *Z <D *A *B map_fs> *Fs <A iter> *Iter do_delete_iter | delete_iter @public -> <D> {
  Z Iter &\ <D> *Y <A iter> *I -> <D> {
    I next >> case {
    ; (| *K, *I' |) value$ -> (Y Fs K do_delete) I' &&
    ; ** -> Y
    }
  }
} ;;

eval <*D> *Z <D *A *B map_fs> *Fs do_iter | iter @public -> <<A B tuple> iter> {
  \\ <<A B tuple> iter_res> { Z Fs do_iter_next } iter$
} ;;

eval <*D> *Z <D *A *B map_fs> *Fs do_iter_next -> <<A B tuple> iter_res> {
  Z Fs do_take >> case {
  ; (| *KV, *Z' |) value$ ->
    (| KV, \\ <<A B tuple> iter_res> { Z' Fs do_iter_next } iter$ |) value$
  ; ** -> none$
  }
} ;;
