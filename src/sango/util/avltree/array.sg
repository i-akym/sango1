###########################################################################
# MIT License                                                             #
# Copyright (c) 2018 Isao Akiyama                                         #
#                                                                         #
# Permission is hereby granted, free of charge, to any person obtaining   #
# a copy of this software and associated documentation files (the         #
# "Software"), to deal in the Software without restriction, including     #
# without limitation the rights to use, copy, modify, merge, publish,     #
# distribute, sublicense, and/or sell copies of the Software, and to      #
# permit persons to whom the Software is furnished to do so, subject to   #
# the following conditions:                                               #
#                                                                         #
# The above copyright notice and this permission notice shall be          #
# included in all copies or substantial portions of the Software.         #
#                                                                         #
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,         #
# EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF      #
# MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.  #
# IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY    #
# CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,    #
# TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE       #
# SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.                  #
###########################################################################
module @alpha "sango.util.avltree.array" ;;

import "sango.util.array" -> array ;;
import "sango.util.avltree.map" -> avlmap ;;
import "sango.util.map" -> map ;;

extend <*A _1> -> my1 :=
  map: <int A map.map>
  compact_: <<A <A bool fun> tuple> maybe>
  array_d$ ;;

eval <int> *Length <*A iter> *Init <<A <A bool fun> tuple> maybe> *Compact_ 
    new_array @public -> <A array.array> {
  Compact_ case {
  ; none$ ->
  ; (| *C, *C?_f |) value$ || C &C?_f ->
  ; ** -> bad_arg$ "Invalid compaction spec." new_exception >> throw
  },
  0 Init (int_ord_fs avlmap.new_map)
      &\ <int> *I <A iter> *S <int A map.map> *M -> <int A map.map> {
    if {
    ; I Length lt? ->
      S next = (| *Val, *S' |) value$,  # must supply next
      Compact_ case {
      ; (| *C, *C?_f |) value$ || Val &C?_f -> (I inc) S' M &&
      ; ** -> (I inc) S' (M I Val map.add) &&
      }
    ; otherwise -> M
    }
  } = *Map,
  map: Map compact_: Compact_ array_d$ = *D,
  length_f: \\ <int> { Length } get_f: ^get set_f: ^set array.array_fs$ = *Fs,
  d: D fs: Fs array.array$
} ;;

eval <*A _1+> *D <int> *Idx get -> <A> {
  D = map: *Map compact_: *Compact_ array_d$,
  Map Idx map.assoc >> case {
  ; *V value$ -> V
  ; ** -> Compact_ = (| *C, ** |) value$, C
  }
} ;;

eval <*A _1+> *D <int> *Idx <A> *Val set -> <A _1+> {
  D = map: *Map compact_: *Compact_ array_d$,
  Compact_ case {
  ; (| **, *C?_f |) value$ || Val &C?_f -> map: (Map Idx map.delete) :: D array_d$
  ; ** -> map: (Map Idx Val map.add) :: D array_d$
  }
} ;;
