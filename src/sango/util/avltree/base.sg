###########################################################################
# MIT License                                                             #
# Copyright (c) 2021 Isao Akiyama                                         #
#                                                                         #
# Permission is hereby granted, free of charge, to any person obtaining   #
# a copy of this software and associated documentation files (the         #
# "Software"), to deal in the Software without restriction, including     #
# without limitation the rights to use, copy, modify, merge, publish,     #
# distribute, sublicense, and/or sell copies of the Software, and to      #
# permit persons to whom the Software is furnished to do so, subject to   #
# the following conditions:                                               #
#                                                                         #
# The above copyright notice and this permission notice shall be          #
# included in all copies or substantial portions of the Software.         #
#                                                                         #
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,         #
# EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF      #
# MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.  #
# IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY    #
# CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,    #
# TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE       #
# SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.                  #
###########################################################################
module @beta "sango.util.avltree.base" ;;

import "sango.arith" -> arith ;;
import "sango.tuple" -> tuple ;;

data <*A tree> @protected :=
  node_: <<A node> maybe>
  aux_builder: <A aux_builder>
  tree$ ;;

data <*A node> @protected :=
  assoc: <A>  # associated data
  aux: <A aux+>  # aux info
  size: <int>
  depth: <int>
  bal: <int>  # left depth - right depth
  left_: <<A node> maybe>
  right_: <<A node> maybe>
  node$ ;;

data <*A aux> @public := dummy_aux$ ;;  # to extend

alias type <*A locator> @public := <<A node> <A locate> fun> ;;

eval <*A> *Assoc <<A node> maybe> *Left_ <<A node> maybe> *Right_ dummy_aux @public -> <A aux> {
  dummy_aux$
} ;;

# assoc x (left node) x (right node) -> aux
alias type <*A aux_builder> @public := <A <<A node> maybe> <<A node> maybe> <A aux+> fun> ;;

data <*A locate> @public :=
| just$
| <A locator> left$
| <A locator> right$
| <cstr> abort$ ;;  # can return reason of abortion

data <*A get_res> @public :=
| <A> get_done$
| get_not_found$
| <cstr> get_aborted$ ;;

data <*A put_res> @public :=
| <A tree> put_new_done$
| <A tree> <A> put_replace_done$
| <cstr> put_aborted$ ;;

data <*A iput_res> :=  # internal
| <A node> iput_new_done$
| <A node> <A> iput_replace_done$
| <cstr> iput_aborted$ ;;

data <*A remove_res> @public :=
| <A tree> <A> remove_done$
| remove_not_found$
| <cstr> remove_aborted$ ;;

data <*A iremove_res> :=  # internal
| <<A node> maybe> <A> iremove_done$
| iremove_not_found$
| <cstr> iremove_aborted$ ;;

eval new_tree @public -> <*A tree> {
  ^dummy_aux new_tree_with_aux
} ;;

eval <*A aux_builder> *AB new_tree_with_aux | new_tree @public -> <A tree> {
  node_: none$
    aux_builder: AB
    tree$
} ;;

eval <*A> *X <A aux_builder> *AB new_leaf_node -> <A node> {
  X none$ none$ AB new_node
} ;;

eval <*A> *X <<A node> maybe> *Left_ <<A node> maybe> *Right_ <A aux_builder> *AB new_node
    -> <A node> {
  Left_ subtree_depth = *LD,
  Right_ subtree_depth = *RD,
  assoc: X
    aux: (X Left_ Right_ &AB)
    size: ((Left_ subtree_size) (Right_ subtree_size) sum >> inc)
    depth: ([ LD, RD ] arith.max >> value >> inc)
    bal: (LD RD diff)
    left_: Left_
    right_: Right_
    node$
} ;;

eval <*A tree> *Tree size @public -> <int> {
  Tree node_ >> subtree_size
} ;;

eval <*A tree> *Tree <A locator> *Loc get @public -> <A get_res> {
  Tree = node_: *Node_ *** tree$,
  Node_ Loc get_in_subtree
} ;;

eval <<*A node> maybe> *Node_ <A locator> *Loc get_in_subtree -> <A get_res> {
  Node_ case {
  ; *Node value$ ->
    Node = assoc: *Assoc left_: *Left_ right_: *Right_ *** node$,
    Node &Loc >> case {
    ; just$ -> Assoc get_done$
    ; *Loc' left$ -> Left_ Loc' &&
    ; *Loc' right$ -> Right_ Loc' &&
    ; *Reason abort$ -> Reason get_aborted$
    }
  ; none$ -> get_not_found$
  }
} ;;

eval <*A tree> *Tree <A locator> *Loc <A> *X put @public -> <A put_res> {
  Tree = node_: *Node_ aux_builder: *AB *** tree$,
  Node_ Loc X AB put_in_subtree >> case {
  ; *Node' iput_new_done$ -> (node_: (Node' value$) :: Tree tree$) put_new_done$
  ; *Node' *Replaced iput_replace_done$ -> (node_: (Node' value$) :: Tree tree$) Replaced put_replace_done$
  ; *Reason iput_aborted$ -> Reason put_aborted$
  }
} ;;

eval <<*A node> maybe> *Node_ <A locator> *Loc <A> *X <A aux_builder> *AB put_in_subtree -> <A iput_res> {
  Node_ case {
  ; *Node value$ ->
    Node = assoc: *Assoc left_: *Left_ right_: *Right_ *** node$,
    Node &Loc >> case { 
    ; just$ ->
      (X Left_ Right_ AB new_node) Assoc iput_replace_done$
    ; *Loc' left$ ->
      Left_ Loc' X AB && >> case {
      ; *Left' iput_new_done$ ->
        ((Assoc (Left' value$) Right_ AB new_node) AB rebalance) iput_new_done$
      ; *Left' *Replaced iput_replace_done$ ->
        ((Assoc (Left' value$) Right_ AB new_node) AB rebalance) Replaced iput_replace_done$
      ; *Aborted -> Aborted
      }
    ; *Loc' right$ ->
      Right_ Loc' X AB && >> case {
      ; *Right' iput_new_done$ ->
        ((Assoc Left_ (Right' value$) AB new_node) AB rebalance) iput_new_done$
      ; *Right' *Replaced iput_replace_done$ ->
        ((Assoc Left_ (Right' value$) AB new_node) AB rebalance) Replaced iput_replace_done$
      ; *Aborted -> Aborted
      }
    ; *Reason abort$ -> Reason iput_aborted$
    }
  ; none$ ->
    (X AB new_leaf_node) iput_new_done$
  }
} ;;

eval <*A tree> *Tree <A locator> *Loc remove @public -> <A remove_res> {
  Tree = node_: *Node_ aux_builder: *AB *** tree$,
  Node_ Loc AB remove_in_subtree >> case {
  ; *Node_' *Removed iremove_done$ -> (node_: Node_' :: Tree tree$) Removed remove_done$
  ; iremove_not_found$ -> remove_not_found$
  ; *Reason iremove_aborted$ -> Reason remove_aborted$
  }
} ;;

eval <<*A node> maybe> *Node_ <A locator> *Loc <A aux_builder> *AB remove_in_subtree -> <A iremove_res> {
  Node_ case {
  ; *Node value$ ->
    Node = assoc: *Assoc left_: *Left_ right_: *Right_ *** node$,
    Node &Loc >> case { 
    ; just$ ->
      (| Left_, Right_ |) case {
      ; (| none$, none$ |) -> none$ Assoc iremove_done$
      ; (| none$, ** |) -> Right_ Assoc iremove_done$
      ; (| **, none$ |) -> Left_ Assoc iremove_done$
      ; (| **, *Right value$ |) ->
        Right AB cut_leftmost = (| *Assoc', *Right_' |),
        (((Assoc' Left_ Right_' AB new_node) AB rebalance) value$) Assoc iremove_done$
      }
    ; *Loc' left$ ->
      Left_ Loc' AB && >> case {
      ; *Left_' *Removed iremove_done$ ->
        (((Assoc Left_' Right_ AB new_node) AB rebalance) value$) Removed iremove_done$
      ; *Not_found_or_aborted -> Not_found_or_aborted
      }
    ; *Loc' right$ ->
      Right_ Loc' AB && >> case {
      ; *Right_' *Removed iremove_done$ ->
        (((Assoc Left_ Right_' AB new_node) AB rebalance) value$) Removed iremove_done$
      ; *Not_found_or_aborted -> Not_found_or_aborted
      }
    ; *Reason abort$ -> Reason iremove_aborted$
    }
  ; none$ -> iremove_not_found$
  }
} ;;

eval <*A tree> *Tree take @public -> <<A <A tree> tuple> maybe> {
  Tree = node_: *Node_ aux_builder: *AB *** tree$,
  Node_ AB take_in_subtree >> case {
  ; (| *X, *Node_' |) value$ -> (| X, node_: Node_' :: Tree tree$ |) value$
  ; none$ -> none$
  }
} ;;

eval <<*A node> maybe> *Node_ <A aux_builder> *AB take_in_subtree
    -> <<A <<A node> maybe> tuple> maybe> {
  Node_ case {
  ; *Node value$ -> (Node AB cut_leftmost) value$
  ; none$ -> none$
  }
} ;;

eval <*A node> *Node <A aux_builder> *AB cut_leftmost -> <A <<A node> maybe> tuple> {
  Node = assoc: *Assoc left_: *Left_ right_: *Right_ *** node$,
  (| Left_, Right_ |) case {
  ; (| none$, none$ |) -> (| Assoc, none$ |)
  ; (| none$, ** |) -> (| Assoc, Right_ |)
  ; (| *Left value$, ** |) ->
    Left AB && = (| *A, *Left_' |),
    (| A, ((Assoc Left_' Right_ AB new_node) AB rebalance) value$ |)
  }
} ;;

eval <*A node> *Node <A aux_builder> *AB rebalance -> <A node> {
  Node bal >> case {
  ; 2 -> Node AB rebalance_heavy_left
  ; 1 -> Node
  ; 0 -> Node
  ; -1 -> Node
  ; -2 -> Node AB rebalance_heavy_right
  }
} ;;

eval <*A node> *Node <A aux_builder> *AB rebalance_heavy_left -> <A node> {
  Node = assoc: *Assoc left_: (*Left value$) right_: *Right_ *** node$,
  Left bal >> case {
  ; 1 -> Node AB rotate_right
  ; 0 -> Node AB rotate_right
  ; -1 -> (Assoc ((Left AB rotate_left) value$) Right_ AB new_node) AB rotate_right
  }
} ;;

eval <*A node> *Node <A aux_builder> *AB rebalance_heavy_right -> <A node> {
  Node = assoc: *Assoc left_: *Left_ right_: (*Right value$) *** node$,
  Right bal >> case {
  ; 1 -> (Assoc Left_ ((Right AB rotate_right) value$) AB new_node) AB rotate_left
  ; 0 -> Node AB rotate_left
  ; -1 -> Node AB rotate_left
  }
} ;;

eval <*A node> *Node <A aux_builder> *AB rotate_left -> <A node> {
  Node = assoc: *Assoc left_: *Left_ right_: (*Right value$) *** node$,
  (Right assoc) ((Assoc Left_ (Right left_) AB new_node) value$) (Right right_) AB new_node
} ;;

eval <*A node> *Node <A aux_builder> *AB rotate_right -> <A node> {
  Node = assoc: *Assoc left_: (*Left value$) right_: *Right_ *** node$,
   (Left assoc) (Left left_) ((Assoc (Left right_) Right_ AB new_node) value$) AB new_node
} ;;

eval <<*A node> maybe> *Node_ subtree_size @public -> <int> {
  Node_ case {
  ; *Node value$ -> Node size
  ; none$ -> 0
  }
} ;;

eval <<*A node> maybe> *Node_ subtree_depth -> <int> {
  Node_ case {
  ; *Node value$ -> Node depth
  ; ** -> 0
  }
} ;;
