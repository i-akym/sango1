###########################################################################
# MIT License                                                             #
# Copyright (c) 2021 AKIYAMA Isao                                         #
#                                                                         #
# Permission is hereby granted, free of charge, to any person obtaining   #
# a copy of this software and associated documentation files (the         #
# "Software"), to deal in the Software without restriction, including     #
# without limitation the rights to use, copy, modify, merge, publish,     #
# distribute, sublicense, and/or sell copies of the Software, and to      #
# permit persons to whom the Software is furnished to do so, subject to   #
# the following conditions:                                               #
#                                                                         #
# The above copyright notice and this permission notice shall be          #
# included in all copies or substantial portions of the Software.         #
#                                                                         #
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,         #
# EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF      #
# MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.  #
# IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY    #
# CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,    #
# TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE       #
# SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.                  #
###########################################################################
module @alpha "sango.util.packaging" ;;

import "sango.entity.box" -> box ;;
import "sango.entity.existence" -> ex ;;
import "sango.util.map.weakkeymap" -> wkm ;;

data <*A package_mgr_h> @opaque :=
  tab: <ex.existence <A v> wkm.map_h>
  package_mgr_h$ ;;

alias type <*A v> := <<A box.box_h> ex.redress_f> ;;

data <tag> @opaque :=
  ex: <ex.existence>
  tag$ ;;

eval tag_dress_fs @public -> <tag ex.dress_fs> {
  undress_f: ^tag_undress ex.dress_fs$
} ;;

eval <tag> *Tag tag_undress -> <ex.existence <tag ex.redress_f> tuple> {
  (| Tag ex, \ <ex.existence> *E -> <tag> { E tag$ } |)
} ;;

eval <*A! A fun> *Content create_package_mgr @public -> <A package_mgr_h> {
  (&\\ <ex.existence ex.dress_fs> { ex.dress_fs })
    (&\\ <<A v> <A v> fun> { ^itself })
    wkm.create_weak_key_map = *Tab,
  Tab package_mgr_h$
} ;;

eval <*A! package_mgr_h> *Mgr <A> *X pack @public -> <tag> {
  X none$ box.create_box = *Pkg,
  Pkg &(box.dress_fs >> ex.undress_f) = (| *Ex, *Redress_f |),
  (Mgr tab) Ex Redress_f wkm.add,
  Ex tag$
} ;;

eval <*A package_mgr_h> *Mgr <tag> *Tag get @public -> <A maybe> {
  Tag ex = *Ex,
  (Mgr tab) Ex wkm.assoc >> case {
  ; *Redress_f value$ -> (Ex &Redress_f >> box.read) value$
  ; ** -> none$
  }
} ;;
