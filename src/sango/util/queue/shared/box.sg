###########################################################################
# MIT License                                                             #
# Copyright (c) 2025 AKIYAMA Isao                                         #
#                                                                         #
# Permission is hereby granted, free of charge, to any person obtaining   #
# a copy of this software and associated documentation files (the         #
# "Software"), to deal in the Software without restriction, including     #
# without limitation the rights to use, copy, modify, merge, publish,     #
# distribute, sublicense, and/or sell copies of the Software, and to      #
# permit persons to whom the Software is furnished to do so, subject to   #
# the following conditions:                                               #
#                                                                         #
# The above copyright notice and this permission notice shall be          #
# included in all copies or substantial portions of the Software.         #
#                                                                         #
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,         #
# EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF      #
# MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.  #
# IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY    #
# CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,    #
# TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE       #
# SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.                  #
###########################################################################
module @alpha "sango.util.queue.shared.box" ;;

import "sango.actor.util" -> actu ;;
import "sango.entity.box" -> box ;;
import "sango.list" -> list ;;
import "sango.util.queue" -> queue ;;
import "sango.util.queue.shared" -> shqueue ;;

eval <*A! queue.queue> *Queue create_box @public -> <A shqueue.shared_queue_h> {
  Queue none$ box.create_box = *Box,
  Box actu.create_box_monitor = *Mon,

  extract_f: \\ <A iter> { Mon extract }
  empty?_f: \\ <bool> { Mon empty? }
  length_f: \\ <int> { Mon length }
  enqueue_f: \ <A> *Elem -> <void> { Mon Elem enqueue }
  head_f: \\ <A maybe> { Mon head }
  dequeue_f: \\ <A maybe> { Mon dequeue }
  clear_f:  \\ <void> { Mon clear }
  shqueue.shared_queue_h$
} ;;

eval <<*A! queue.queue> actu.box_monitor_h> *Mon extract -> <A iter> {
  Mon
    \ <<A queue.queue> box.box_h> *Box -> <A queue.queue> {
      Box box.read
    }
    actu.run = *Z,
  Z queue.iter
} ;;

eval <<*A! queue.queue> actu.box_monitor_h> *Mon empty? -> <bool> {
  Mon
    \ <<A queue.queue> box.box_h> *Box -> <A queue.queue> {
      Box box.read
    } actu.run = *Z,
  Z queue.empty?
} ;;

eval <<*A! queue.queue> actu.box_monitor_h> *Mon length -> <int> {
  Mon
    \ <<A queue.queue> box.box_h> *Box -> <A queue.queue> {
      Box box.read
    } actu.run = *Z,
  Z queue.length
} ;;

eval <<*A! queue.queue> actu.box_monitor_h> *Mon <A> *Elem enqueue -> <void> {
  Mon
    \ <<A queue.queue> box.box_h> *Box -> <void> {
      Box box.read = *Z,
      Z Elem queue.enqueue = *Z',
      Box Z' box.write >> ignore
    } actu.run
} ;;

eval <<*A! queue.queue> actu.box_monitor_h> *Mon head -> <A maybe> {
  Mon
    \ <<A queue.queue> box.box_h> *Box -> <A queue.queue> {
      Box box.read
    } actu.run = *Z,
  Z queue.head
} ;;

eval <<*A! queue.queue> actu.box_monitor_h> *Mon dequeue -> <A maybe> {
  Mon
    \ <<A queue.queue> box.box_h> *Box -> <A maybe> {
      Box box.read = *Z,
      Z queue.dequeue >> case {
      ; (| *Dequeued, *Z' |) value$ ->
        Box Z' box.write,
        Dequeued value$
      ; ** -> 
        none$
      }
    } actu.run
} ;;

eval <<*A! queue.queue> actu.box_monitor_h> *Mon clear -> <void> {
  Mon
    \ <<A queue.queue> box.box_h> *Box -> <void> {
      Box box.read = *Z,
      Z queue.clear = *Z',
      Box Z' box.write >> ignore
    } actu.run
} ;;
