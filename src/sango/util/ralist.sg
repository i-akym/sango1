###########################################################################
# MIT License                                                             #
# Copyright (c) 2021 AKIYAMA Isao                                         #
#                                                                         #
# Permission is hereby granted, free of charge, to any person obtaining   #
# a copy of this software and associated documentation files (the         #
# "Software"), to deal in the Software without restriction, including     #
# without limitation the rights to use, copy, modify, merge, publish,     #
# distribute, sublicense, and/or sell copies of the Software, and to      #
# permit persons to whom the Software is furnished to do so, subject to   #
# the following conditions:                                               #
#                                                                         #
# The above copyright notice and this permission notice shall be          #
# included in all copies or substantial portions of the Software.         #
#                                                                         #
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,         #
# EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF      #
# MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.  #
# IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY    #
# CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,    #
# TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE       #
# SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.                  #
###########################################################################
module "sango.util.ralist" ;;

import "sango.list" -> list ;;
import "sango.tuple" -> tuple ;;
import "sango.util.iter" -> iter ;;

### function set

data <*D *A ralist_fs> @public :=
  empty?_f: <D bool fun>
  length_f: <D int fun>
  elem_f: <D int <A maybe> fun>
  first_f_: <<D <A maybe> fun> maybe>
  last_f_: <<D <A maybe> fun> maybe>
  tail_f_: <<D <D maybe> fun> maybe>
  insert_at_f: <D int A D fun>
  append_f_: <<D A D fun> maybe>
  remove_at_f: <D int <D <A maybe> tuple> fun>
  replace_at_f_: <<D int A <D <A maybe> tuple> fun> maybe>
  take_f_: <<D int D fun> maybe>
  drop_f_: <<D int D fun> maybe>
  clear_f: <D D fun>
  ralist_fs$ ;;

eval <*D *A ralist_fs> *Fs ralist_fs_first_f | first_f @public -> <D <A maybe> fun> {
  Fs first_f_ >> case {
  ; *F value$ -> F
  ; none$ ->
    \ <D> *LD -> <A maybe> {
      LD 0 &(Fs elem_f)
    }
  }
} ;;

eval <*D *A ralist_fs> *Fs ralist_fs_last_f | last_f @public -> <D <A maybe> fun> {
  Fs last_f_ >> case {
  ; *F value$ -> F
  ; none$ ->
    \ <D> *LD -> <A maybe> {
      LD (LD &(Fs length_f) >> dec) &(Fs elem_f)
    }
  }
} ;;

eval <*D *A ralist_fs> *Fs ralist_fs_tail_f | tail_f @public -> <D <D maybe> fun> {
  Fs tail_f_ >> case {
  ; *F value$ -> F
  ; none$ ->
    \ <D> *LD -> <D maybe> {
      LD &(Fs empty?_f) >> case {
      ; true$ -> none$
      ; ** -> (LD 0 &(Fs delete_at_f)) value$
      }
    }
  }
} ;;

eval <*D *A ralist_fs> *Fs ralist_fs_append_f | append_f @public -> <D A D fun> {
  Fs append_f_ >> case {
  ; *F value$ -> F
  ; none$ ->
    \ <D> *LD <A> *X -> <D> {
      LD (LD &(Fs length_f)) X &(Fs insert_at_f)
    }
  }
} ;;

eval <*D *A ralist_fs> *Fs ralist_fs_delete_at_f | delete_at_f @public -> <D int D fun> {
  \ <D> *LD <int> *Index -> <D> {
    LD Index &(Fs remove_at_f) >> tuple.pair_first
  }
} ;;

eval <*D *A ralist_fs> *Fs ralist_fs_replace_at_f | replace_at_f @public -> <D int A <D <A maybe> tuple> fun> {
  Fs replace_at_f_ >> case {
  ; *F value$ -> F
  ; none$ ->
    \ <D> *LD <int> *Index <A> *X -> <D <A maybe> tuple> {
      LD Index &(Fs remove_at_f) >> case {
      ; (| *LD', *Removed value$ |) -> (| LD' Index X &(Fs insert_at_f), Removed value$ |)
      ; ** -> (| LD, none$ |)
      }
    }
  }
} ;;

eval <*D *A ralist_fs> *Fs ralist_fs_update_at_f | update_at_f @public -> <D int A D fun> {
  \ <D> *LD <int> *Index <A> *Elem -> <D> {
    LD Index Elem &(Fs replace_at_f) >> tuple.pair_first
  }
} ;;

eval <*D *A ralist_fs> *Fs ralist_fs_take_f | take_f @public -> <<D> int <D> fun> {
  Fs take_f_ >> case {
  ; *F value$ -> F
  ; none$ ->
    \ <D> *LD <int> *Count -> <D> {
      if {
      ; Count ge0? ->
      ; otherwise -> bad_arg$ "Negative count." new_exception >> throw
      },
      Fs empty?_f = *Empty?_f,
      Fs first_f = *First_f,
      Fs tail_f = *Tail_f,
      Fs append_f = *Append_f,
      Count LD (LD &(Fs clear_f))
          &\ <int> *CC <D> *LD_from <D> *LD_to -> <D> {
        (| LD_from &Empty?_f, CC |) case {
        ; (| true$, ** |) -> LD_to
        ; (| **, 0 |) -> LD_to
        ; ** -> (CC dec) (LD_from &Tail_f >> value) (LD_to (LD_from &First_f >> value) &Append_f) &&
        }
      }
    }
  }
} ;;

eval <*D *A ralist_fs> *Fs ralist_fs_drop_f | drop_f @public -> <D int D fun> {
  Fs take_f_ >> case {
  ; *F value$ -> F
  ; none$ ->
    \ <D> *LD <int> *Count -> <D> {
      if {
      ; Count ge0? ->
      ; otherwise -> bad_arg$ "Negative count." new_exception >> throw
      },
      Fs empty?_f = *Empty?_f,
      Fs tail_f = *Tail_f,
      Count LD &\ <int> *CC <D> *DD -> <D> {
        (| DD &Empty?_f, CC |) case {
        ; (| true$, ** |) -> DD
        ; (| **, 0 |) -> DD
        ; ** -> (CC dec) (DD &Tail_f >> value) &&
        }
      }
    }
  }
} ;;

eval <*D *A ralist_fs> *Fs ralist_fs_to_col_fs | to_col_fs @public -> <D A col_fs> {
  Fs = empty?_f: *Empty_f length_f: *Length_f *** ralist_fs$,
  empty?_f_: (Empty_f value$)
  size_f: Length_f
  iter_f: \ <D> *Z -> <A iter> { Z Fs do_iter }
  col_fs$
} ;;


### generic

data <*D *A gralist> @public :=
  d: <D>
  fs: <D A ralist_fs>
  gralist$
++ a'ralist_impl -> [ A a'ralist ] ;;

alias type <*A d> @public := <A _1+> ;;
alias type <*A ralist> @public := <<A d> A gralist> ;;

eval a'ralist_impl -> <<*D *A gralist> A ralist_fs> {
  empty?_f: ^g_empty?
  length_f: ^g_length
  elem_f: ^g_elem
  first_f_: (^g_first value$)
  last_f_: (^g_last value$)
  tail_f_: (^g_tail value$)
  insert_at_f: ^g_insert_at
  append_f_: (^g_append value$)
  remove_at_f: ^g_remove_at
  replace_at_f_: (^g_replace_at value$)
  take_f_: (^g_take value$)
  drop_f_: (^g_drop value$)
  clear_f: ^g_clear
  ralist_fs$
} ;;

eval <*D *A gralist> *Ralist g_empty? | empty? @public -> <bool> {
  Ralist = d: *D fs: *Fs gralist$,
  D Fs do_empty?
} ;;

eval <*D *A gralist> *Ralist g_non_empty? | non_empty? @public -> <bool> {
  Ralist g_empty? >> not
} ;;

eval <*D *A gralist> *Ralist g_length | length @public -> <int> {
  Ralist = d: *D fs: *Fs gralist$,
  D Fs do_length
} ;;

eval <*D *A gralist> *Ralist <int> *Index g_elem | elem @public -> <A maybe> {
  Ralist = d: *D fs: *Fs gralist$,
  D Fs Index do_elem
} ;;

eval <*D *A gralist> *Ralist g_first | first | head @public -> <A maybe> {
  Ralist = d: *D fs: *Fs gralist$,
  D Fs do_first
} ;;

eval <*D *A gralist> *Ralist g_last | last @public -> <A maybe> {
  Ralist = d: *D fs: *Fs gralist$,
  D Fs do_last
} ;;

eval <*D *A gralist> *Ralist g_tail | tail @public -> <<D A gralist> maybe> {
  Ralist = d: *D fs: *Fs gralist$,
  D Fs do_tail >> case {
  ; *D' value$ -> (d: D' fs: Fs gralist$) value$
  ; ** -> none$
  }
} ;;

eval <*D *A gralist> *Ralist <int> *Index <A> *Elem g_insert_at | insert_at @public -> <D A gralist> {
  Ralist = d: *D fs: *Fs gralist$,
  D Fs Index Elem do_insert_at = *D',
  d: D' fs: Fs gralist$
} ;;

eval <*D *A gralist> *Ralist <A> *Elem g_append | append @public -> <D A gralist> {
  Ralist = d: *D fs: *Fs gralist$,
  D Fs Elem do_append = *D',
  d: D' fs: Fs gralist$
} ;;

eval <*D *A gralist> *Ralist <int> *Index g_remove_at | remove_at @public -> <<D A gralist> <A maybe> tuple> {
  Ralist = d: *D fs: *Fs gralist$,
  D Fs Index do_remove_at >> case {
  ; (| *D', *Removed value$ |) -> (| d: D' fs: Fs gralist$, Removed value$ |)
  ; ** -> (| Ralist, none$ |)
  }
} ;;

eval <*D *A gralist> *Ralist <int> *Index g_delete_at | delete_at @public -> <D A gralist> {
  Ralist = d: *D fs: *Fs gralist$,
  D Fs Index do_delete_at = *D',
  d: D' fs: Fs gralist$
} ;;

eval <*D *A gralist> *Ralist <int> *Index <A> *Elem g_replace_at | replace_at @public -> <<D A gralist> <A maybe> tuple> {
  Ralist = d: *D fs: *Fs gralist$,
  D Fs Index Elem do_replace_at >> case {
  ; (| *D', *Replaced value$ |) -> (| d: D' fs: Fs gralist$, Replaced value$ |)
  ; ** -> (| Ralist, none$ |)
  }
} ;;

eval <*D *A gralist> *Ralist <int> *Index <A> *Elem g_update_at | update_at @public -> <D A gralist> {
  Ralist = d: *D fs: *Fs gralist$,
  D Fs Index Elem do_update_at = *D',
  d: D' fs: Fs gralist$
} ;;

eval <*D *A gralist> *Ralist <int> *Count g_take | take @public -> <D A gralist> {
  Ralist = d: *D fs: *Fs gralist$,
  D Fs Count do_take = *D',
  d: D' fs: Fs gralist$
} ;;

eval <*D *A gralist> *Ralist <int> *Count g_drop | drop @public -> <D A gralist> {
  Ralist = d: *D fs: *Fs gralist$,
  D Fs Count do_drop = *D',
  d: D' fs: Fs gralist$
} ;;

eval <*D *A gralist> *Ralist g_clear | clear @public -> <D A gralist> {
  Ralist = d: *D fs: *Fs gralist$,
  D Fs do_clear = *D',
  d: D' fs: Fs gralist$
} ;;

eval <*D *A gralist> *Ralist <A list> *Elems g_append_list | append_list @public -> <D A gralist> {
  Ralist (Elems list.iter) g_append_iter
} ;;

eval <*D *A gralist> *Ralist <A iter> *Iter g_append_iter | append_iter @public -> <D A gralist> {
  Ralist = d: *D fs: *Fs gralist$,
  D Fs Iter do_append_iter = *D',
  d: D' fs: Fs gralist$
} ;;

eval <*D *A gralist> *Ralist g_iter | iter @public -> <A iter> {
  Ralist = d: *D fs: *Fs gralist$,
  D Fs do_iter
} ;;


### feature

feature <*T> [ *A a'ralist ] @public -> <T A ralist_fs> ;;

eval <*T[ *A a'ralist ]> *FRalist f_empty? | empty? @public -> <bool> {
  FRalist _feature_a'ralist = *Ralist_fs,
  FRalist Ralist_fs do_empty?
} ;;

eval <*T[ *A a'ralist ]> *FRalist f_non_empty? | non_empty? @public -> <bool> {
  FRalist f_empty? >> not
} ;;

eval <*T[ *A a'ralist ]> *FRalist f_length | length @public -> <int> {
  FRalist _feature_a'ralist = *Ralist_fs,
  FRalist Ralist_fs do_length
} ;;

eval <*T[ *A a'ralist ]> *FRalist <int> *Index f_elem | elem @public -> <A maybe> {
  FRalist _feature_a'ralist = *Ralist_fs,
  FRalist Ralist_fs Index do_elem
} ;;

eval <*T[ *A a'ralist ]> *FRalist <int> *Index f_first | first | head @public -> <A maybe> {
  FRalist _feature_a'ralist = *Ralist_fs,
  FRalist Ralist_fs do_first
} ;;

eval <*T[ *A a'ralist ]> *FRalist <int> *Index f_last | last @public -> <A maybe> {
  FRalist _feature_a'ralist = *Ralist_fs,
  FRalist Ralist_fs do_last
} ;;

eval <*T[ *A a'ralist ]> *FRalist f_tail | tail @public -> <T maybe> {
  FRalist _feature_a'ralist = *Ralist_fs,
  FRalist Ralist_fs do_tail
} ;;

eval <*T[ *A a'ralist ]> *FRalist <int> *Index <A> *Elem f_insert_at | insert_at @public -> <T> {
  FRalist _feature_a'ralist = *Ralist_fs,
  FRalist Ralist_fs Index Elem do_insert_at
} ;;

eval <*T[ *A a'ralist ]> *FRalist <A> *Elem f_append | append @public -> <T> {
  FRalist _feature_a'ralist = *Ralist_fs,
  FRalist Ralist_fs Elem do_append
} ;;

eval <*T[ *A a'ralist ]> *FRalist <int> *Index f_remove_at | remove_at @public -> <T <A maybe> tuple> {
  FRalist _feature_a'ralist = *Ralist_fs,
  FRalist Ralist_fs Index do_remove_at
} ;;

eval <*T[ *A a'ralist ]> *FRalist <int> *Index f_delete_at | delete_at @public -> <T> {
  FRalist _feature_a'ralist = *Ralist_fs,
  FRalist Ralist_fs Index do_delete_at
} ;;

eval <*T[ *A a'ralist ]> *FRalist <int> *Index <A> *Elem f_replace_at | replace_at @public -> <T <A maybe> tuple> {
  FRalist _feature_a'ralist = *Ralist_fs,
  FRalist Ralist_fs Index Elem do_replace_at
} ;;

eval <*T[ *A a'ralist ]> *FRalist <int> *Index <A> *Elem f_update_at | update_at @public -> <T> {
  FRalist _feature_a'ralist = *Ralist_fs,
  FRalist Ralist_fs Index Elem do_update_at
} ;;

eval <*T[ *A a'ralist ]> *FRalist <int> *Count f_take | take @public -> <T> {
  FRalist _feature_a'ralist = *Ralist_fs,
  FRalist Ralist_fs Count do_take
} ;;

eval <*T[ *A a'ralist ]> *FRalist <int> *Count f_drop | drop @public -> <T> {
  FRalist _feature_a'ralist = *Ralist_fs,
  FRalist Ralist_fs Count do_drop
} ;;

eval <*T[ *A a'ralist ]> *FRalist f_clear | clear @public -> <T> {
  FRalist _feature_a'ralist = *Ralist_fs,
  FRalist Ralist_fs do_clear
} ;;

eval <*T[ *A a'ralist ]> *FRalist <A list> *Elems f_append_list | append_list @public -> <T> {
  FRalist (Elems list.iter) f_append_iter
} ;;

eval <*T[ *A a'ralist ]> *FRalist <A iter> *Iter f_append_iter | append_iter @public -> <T> {
  FRalist _feature_a'ralist = *Ralist_fs,
  FRalist Ralist_fs Iter do_append_iter
} ;;

eval <*T[ *A a'ralist ]> *FRalist f_iter | iter @public -> <A iter> {
  FRalist _feature_a'ralist = *Ralist_fs,
  FRalist Ralist_fs do_iter
} ;;


### incarnation

data <*T *A ralist_inc> @protected :=
  org: <T>
  fo: <T A ralist_fs>
  ralist_inc$
++ inc_a'col_impl -> [ A a'col ] ;;

eval <*T[ *A a'ralist ]> *FRalist incarnate @public -> <T A ralist_inc> {
  org: FRalist
  fo: (FRalist _feature_a'ralist)
  ralist_inc$
} ;;

eval inc_a'col_impl -> <<*T *A ralist_inc> A col_fs> {
  empty?_f_:
    (\ <*TT *AA ralist_inc> *I -> <bool> {
       I = *ID *IFs ralist_inc$,
       ID IFs do_empty?
     } value$)
  size_f:
    \ <*TT *AA ralist_inc> *I -> <int> {
      I = *ID *IFs ralist_inc$,
      ID IFs do_length
    }
  iter_f:
    \ <*TT *AA ralist_inc> *I -> <AA iter> {
      I = *ID *IFs ralist_inc$,
      ID IFs do_iter
    }
  col_fs$
} ;;


### subroutines

eval <*D> *Z <D *A ralist_fs> *Fs do_empty? @public -> <bool> {
  Z &(Fs empty?_f)
} ;;

eval <*D> *Z <D *A ralist_fs> *Fs do_length @public -> <int> {
  Z &(Fs length_f)
} ;;

eval <*D> *Z <D *A ralist_fs> *Fs <int> *Index do_elem @public -> <A maybe> {
  Z Index &(Fs elem_f)
} ;;

eval <*D> *Z <D *A ralist_fs> *Fs do_first @public -> <A maybe> {
  Z &(Fs first_f)
} ;;

eval <*D> *Z <D *A ralist_fs> *Fs do_last @public -> <A maybe> {
  Z &(Fs last_f)
} ;;

eval <*D> *Z <D *A ralist_fs> *Fs do_tail @public -> <D maybe> {
  Z &(Fs tail_f)
} ;;

eval <*D> *Z <D *A ralist_fs> *Fs <int> *Index <A> *Elem do_insert_at @public -> <D> {
  Z Index Elem &(Fs insert_at_f)
} ;;

eval <*D> *Z <D *A ralist_fs> *Fs <A> *Elem do_append @public -> <D> {
  Z Elem &(Fs append_f)
} ;;

eval <*D> *Z <D *A ralist_fs> *Fs <int> *Index do_remove_at @public -> <D <A maybe> tuple> {
  Z Index &(Fs remove_at_f)
} ;;

eval <*D> *Z <D *A ralist_fs> *Fs <int> *Index do_delete_at @public -> <D> {
  Z Index &(Fs delete_at_f)
} ;;

eval <*D> *Z <D *A ralist_fs> *Fs <int> *Index <A> *Elem do_replace_at @public -> <D <A maybe> tuple> {
  Z Index Elem &(Fs replace_at_f)
} ;;

eval <*D> *Z <D *A ralist_fs> *Fs <int> *Index <A> *Elem do_update_at @public -> <D> {
  Z Index Elem &(Fs update_at_f)
} ;;

eval <*D> *Z <D *A ralist_fs> *Fs <int> *Count do_take @public -> <D> {
  Z Count &(Fs take_f)
} ;;

eval <*D> *Z <D *A ralist_fs> *Fs <int> *Count do_drop @public -> <D> {
  Z Count &(Fs drop_f)
} ;;

eval <*D> *Z <D *A ralist_fs> *Fs do_clear @public -> <D> {
  Z &(Fs clear_f)
} ;;

eval <*D> *Z <D *A ralist_fs> *Fs <A list> *Elems do_append_list | append_list @public -> <D> {
  Z Fs (Elems list.iter) do_append_iter
} ;;

eval <*D> *Z <D *A ralist_fs> *Fs <A iter> *Iter do_append_iter | append_iter @public -> <D> {
  Z
  Iter
  \ <D> *Y <A> *E -> <D> { Y Fs E do_append }
  iter.fold
} ;;

eval <*D> *Z <D *A ralist_fs> *Fs do_iter | iter @public -> <A iter> {
  \\ <A iter_res> { Z Fs do_iter_next } iter$
} ;;

eval <*D> *Z <D *A ralist_fs> *Fs do_iter_next -> <A iter_res> {
  if {
  ; Z Fs do_empty? -> none$
  ; otherwise ->
    (| Z Fs do_first >> value, \\ <A iter_res> { (Z Fs do_tail >> value) Fs do_iter_next } iter$ |) value$
  }
} ;;
