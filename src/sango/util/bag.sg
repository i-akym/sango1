###########################################################################
# MIT License                                                             #
# Copyright (c) 2018 Isao Akiyama                                         #
#                                                                         #
# Permission is hereby granted, free of charge, to any person obtaining   #
# a copy of this software and associated documentation files (the         #
# "Software"), to deal in the Software without restriction, including     #
# without limitation the rights to use, copy, modify, merge, publish,     #
# distribute, sublicense, and/or sell copies of the Software, and to      #
# permit persons to whom the Software is furnished to do so, subject to   #
# the following conditions:                                               #
#                                                                         #
# The above copyright notice and this permission notice shall be          #
# included in all copies or substantial portions of the Software.         #
#                                                                         #
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,         #
# EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF      #
# MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.  #
# IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY    #
# CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,    #
# TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE       #
# SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.                  #
###########################################################################
module "sango.util.bag" ;;

import "sango.list" -> list ;;
import "sango.util.iter" -> iter ;;

### function set

data <*D *A bag_fs> @public :=
  empty?_f: <D bool fun>
  size_f: <D int fun>
  contain?_f: <D A bool fun>
  get_all_f: <D A <A list> fun>
  add_f: <D A D fun>
  remove_f: <D A <D <A maybe> tuple> fun>
  remove_all_f: <D A <D <A list> tuple> fun>
  delete_f_: <<D A D fun> maybe>
  delete_all_f_: <<D A D fun> maybe>
  take_f: <D <<A D tuple> maybe> fun>
  clear_f: <D D fun>
  bag_fs$ ;;

eval <*D *A bag_fs> *Fs bag_fs_delete_f | delete_f @public -> <D A D fun> {
  Fs delete_f_ >> case {
  ; *F value$ -> F
  ; none$ ->
    \ <D> *Z <A> *Elem -> <D> {
      Z Elem &(Fs remove_f) = (| *Z', ** |),
      Z'
    }
  }
} ;;

eval <*D *A bag_fs> *Fs bag_fs_delete_all_f | delete_all_f @public -> <D A D fun> {
  Fs delete_all_f_ >> case {
  ; *F value$ -> F
  ; none$ ->
    \ <D> *Z <A> *Elem -> <D> {
      Z Elem &(Fs remove_all_f) = (| *Z', ** |),
      Z'
    }
  }
} ;;

eval <*D *A bag_fs> *Fs bag_fs_to_col_fs | to_col_fs @public -> <D A col_fs> {
  Fs = empty?_f: *Empty_f size_f: *Size_f *** bag_fs$,
  empty?_f_: (Empty_f value$)
  size_f: Size_f
  iter_f: \ <D> *Z -> <A iter> { Z Fs do_iter }
  col_fs$
} ;;


### generic

data <*D *A gbag> @public :=
  d: <D>
  fs: <D A bag_fs>
  gbag$
++ a'bag_impl -> [ A a'bag ] ;;

alias type <*A d> @public := <A _1+> ;;
alias type <*A bag> @public := <<A d> A gbag> ;;

eval a'bag_impl -> <<*D *A gbag> A bag_fs> {
  empty?_f: ^g_empty?
  size_f: ^g_size
  contain?_f: ^g_contain?
  get_all_f: ^g_get_all
  add_f: ^g_add
  remove_f: ^g_remove
  remove_all_f: ^g_remove_all
  delete_f_: (^g_delete value$)
  delete_all_f_: (^g_delete_all value$)
  take_f: ^g_take
  clear_f: ^g_clear
  bag_fs$
} ;;

eval <*D *A gbag> *Bag g_empty? | empty? @public -> <bool> {
  Bag = d: *D fs: *Fs gbag$,
  D Fs do_empty?
} ;;

eval <*D *A gbag> *Bag g_size | size @public -> <int> {
  Bag = d: *D fs: *Fs gbag$,
  D Fs do_size
} ;;

eval <*D *A gbag> *Bag <A> *Elem g_contain? | contain? @public -> <bool> {
  Bag = d: *D fs: *Fs gbag$,
  D Fs Elem do_contain?
} ;;

eval <*D *A gbag> *Bag <A> *Elem g_get_all | get_all @public -> <A list> {
  Bag = d: *D fs: *Fs gbag$,
  D Fs Elem do_get_all
} ;;

eval <*D *A gbag> *Bag <A> *Elem g_add | add @public -> <D A gbag> {
  Bag = d: *D fs: *Fs gbag$,
  D Fs Elem do_add = *D',
  d: D' fs: Fs gbag$
} ;;

eval <*D *A gbag> *Bag <A> *Elem g_remove | remove @public -> <<D A gbag> <A maybe> tuple> {
  Bag = d: *D fs: *Fs gbag$,
  D Fs Elem do_remove = (| *D', *Removed_ |),
  (| d: D' fs: Fs gbag$, Removed_ |)
} ;;

eval <*D *A gbag> *Bag <A> *Elem g_remove_all | remove_all @public -> <<D A gbag> <A list> tuple> {
  Bag = d: *D fs: *Fs gbag$,
  D Fs Elem do_remove_all = (| *D', *Removed_list |),
  (| d: D' fs: Fs gbag$, Removed_list |)
} ;;

eval <*D *A gbag> *Bag <A> *Elem g_delete | delete @public -> <D A gbag> {
  Bag = d: *D fs: *Fs gbag$,
  D Fs Elem do_delete = *D',
  d: D' fs: Fs gbag$
} ;;

eval <*D *A gbag> *Bag <A> *Elem g_delete_all | delete_all @public -> <D A gbag> {
  Bag = d: *D fs: *Fs gbag$,
  D Fs Elem do_delete_all = *D',
  d: D' fs: Fs gbag$
} ;;

eval <*D *A gbag> *Bag g_take | take @public -> <<A <D A gbag> tuple> maybe> {
  Bag = d: *D fs: *Fs gbag$,
  D Fs do_take >> case {
  ; (| *Elem, *D' |) value$ -> (| Elem, d: D' fs: Fs gbag$ |) value$
  ; ** -> none$
  }
} ;;

eval <*D *A gbag> *Bag g_clear | clear @public -> <D A gbag> {
  Bag = d: *D fs: *Fs gbag$,
  D Fs do_clear = *D',
  d: D' fs: Fs gbag$
} ;;

eval <*D *A gbag> *Bag <A list> *Elems g_add_list | add_list @public -> <D A gbag> {
  Bag (Elems list.iter) g_add_iter
} ;;

eval <*D *A gbag> *Bag <A iter> *Iter g_add_iter | add_iter @public -> <D A gbag> {
  Bag = d: *D fs: *Fs gbag$,
  D Fs Iter do_add_iter = *D',
  d: D' fs: Fs gbag$
} ;;

eval <*D *A gbag> *Bag <A list> *Elems g_delete_list | delete_list @public -> <D A gbag> {
  Bag (Elems list.iter) g_delete_iter
} ;;

eval <*D *A gbag> *Bag <A iter> *Iter g_delete_iter | delete_iter @public -> <D A gbag> {
  Bag = d: *D fs: *Fs gbag$,
  D Fs Iter do_delete_iter = *D',
  d: D' fs: Fs gbag$
} ;;

eval <*D *A gbag> *Bag <A list> *Elems g_delete_all_list | delete_all_list @public -> <D A gbag> {
  Bag (Elems list.iter) g_delete_all_iter
} ;;

eval <*D *A gbag> *Bag <A iter> *Iter g_delete_all_iter | delete_all_iter @public -> <D A gbag> {
  Bag = d: *D fs: *Fs gbag$,
  D Fs Iter do_delete_all_iter = *D',
  d: D' fs: Fs gbag$
} ;;

eval <*D *A gbag> *Bag g_iter | iter @public -> <A iter> {
  Bag = d: *D fs: *Fs gbag$,
  D Fs do_iter
} ;;


### feature

feature <*T> [ *A a'bag ] @public -> <T A bag_fs> ;;

eval <*T[ *A a'bag ]> *FBag f_empty? | empty? @public -> <bool> {
  FBag _feature_a'bag = *Bag_fs,
  FBag Bag_fs do_empty?
} ;;

eval <*T[ *A a'bag ]> *FBag f_size | size @public -> <int> {
  FBag _feature_a'bag = *Bag_fs,
  FBag Bag_fs do_size
} ;;

eval <*T[ *A a'bag ]> *FBag <A> *Elem f_contain? | contain? @public -> <bool> {
  FBag _feature_a'bag = *Bag_fs,
  FBag Bag_fs Elem do_contain?
} ;;

eval <*T[ *A a'bag ]> *FBag <A> *Elem f_get_all | get_all @public -> <A list> {
  FBag _feature_a'bag = *Bag_fs,
  FBag Bag_fs Elem do_get_all
} ;;

eval <*T[ *A a'bag ]> *FBag <A> *Elem f_add | add @public -> <T> {
  FBag _feature_a'bag = *Bag_fs,
  FBag Bag_fs Elem do_add
} ;;

eval <*T[ *A a'bag ]> *FBag <A> *Elem f_remove | remove @public -> <T <A maybe> tuple> {
  FBag _feature_a'bag = *Bag_fs,
  FBag Bag_fs Elem do_remove
} ;;

eval <*T[ *A a'bag ]> *FBag <A> *Elem f_remove_all | remove_all @public -> <T <A list> tuple> {
  FBag _feature_a'bag = *Bag_fs,
  FBag Bag_fs Elem do_remove_all
} ;;

eval <*T[ *A a'bag ]> *FBag <A> *Elem f_delete | delete @public -> <T> {
  FBag _feature_a'bag = *Bag_fs,
  FBag Bag_fs Elem do_delete
} ;;

eval <*T[ *A a'bag ]> *FBag <A> *Elem f_delete_all | delete_all @public -> <T> {
  FBag _feature_a'bag = *Bag_fs,
  FBag Bag_fs Elem do_delete_all
} ;;

eval <*T[ *A a'bag ]> *FBag f_take | take @public -> <<A T tuple> maybe> {
  FBag _feature_a'bag = *Bag_fs,
  FBag Bag_fs do_take
} ;;

eval <*T[ *A a'bag ]> *FBag f_clear | clear @public -> <T> {
  FBag _feature_a'bag = *Bag_fs,
  FBag Bag_fs do_clear
} ;;

eval <*T[ *A a'bag ]> *FBag <A list> *Elems f_add_list | add_list @public -> <T> {
  FBag (Elems list.iter) f_add_iter
} ;;

eval <*T[ *A a'bag ]> *FBag <A iter> *Iter f_add_iter | add_iter @public -> <T> {
  FBag _feature_a'bag = *Bag_fs,
  FBag Bag_fs Iter do_add_iter
} ;;

eval <*T[ *A a'bag ]> *FBag <A list> *Elems f_delete_list | delete_list @public -> <T> {
  FBag (Elems list.iter) f_delete_iter
} ;;

eval <*T[ *A a'bag ]> *FBag <A iter> *Iter f_delete_iter | delete_iter @public -> <T> {
  FBag _feature_a'bag = *Bag_fs,
  FBag Bag_fs Iter do_delete_iter
} ;;

eval <*T[ *A a'bag ]> *FBag <A list> *Elems f_delete_all_list | delete_all_list @public -> <T> {
  FBag (Elems list.iter) f_delete_all_iter
} ;;

eval <*T[ *A a'bag ]> *FBag <A iter> *Iter f_delete_all_iter | delete_all_iter @public -> <T> {
  FBag _feature_a'bag = *Bag_fs,
  FBag Bag_fs Iter do_delete_all_iter
} ;;

eval <*T[ *A a'bag ]> *FBag f_iter | iter @public -> <A iter> {
  FBag _feature_a'bag = *Bag_fs,
  FBag Bag_fs do_iter
} ;;


### incarnation

data <*T *A bag_inc> @protected :=
  org: <T>
  fo: <T A bag_fs>
  bag_inc$
++ inc_a'col_impl -> [ A a'col ] ;;

eval <*T[ *A a'bag ]> *FBag incarnate @public -> <T A bag_inc> {
  org: FBag
  fo: (FBag _feature_a'bag)
  bag_inc$
} ;;

eval inc_a'col_impl -> <<*T *A bag_inc> A col_fs> {
  empty?_f_:
    (\ <*TT *AA bag_inc> *I -> <bool> {
       I = *ID *IFs bag_inc$,
       ID IFs do_empty?
     } value$)
  size_f:
    \ <*TT *AA bag_inc> *I -> <int> {
      I = *ID *IFs bag_inc$,
      ID IFs do_size
    }
  iter_f:
    \ <*TT *AA bag_inc> *I -> <AA iter> {
      I = *ID *IFs bag_inc$,
      ID IFs do_iter
    }
  col_fs$
} ;;


### subroutines

eval <*D> *Z <D *A bag_fs> *Fs do_empty? @public -> <bool> {
  Z &(Fs empty?_f)
} ;;

eval <*D> *Z <D *A bag_fs> *Fs do_size @public -> <int> {
  Z &(Fs size_f)
} ;;

eval <*D> *Z <D *A bag_fs> *Fs <A> *Elem do_contain? @public -> <bool> {
  Z Elem &(Fs contain?_f)
} ;;

eval <*D> *Z <D *A bag_fs> *Fs <A> *Elem do_get_all @public -> <A list> {
  Z Elem &(Fs get_all_f)
} ;;

eval <*D> *Z <D *A bag_fs> *Fs <A> *Elem do_add @public -> <D> {
  Z Elem &(Fs add_f)
} ;;

eval <*D> *Z <D *A bag_fs> *Fs <A> *Elem do_remove @public -> <D <A maybe> tuple> {
  Z Elem &(Fs remove_f)
} ;;

eval <*D> *Z <D *A bag_fs> *Fs <A> *Elem do_remove_all @public -> <D <A list> tuple> {
  Z Elem &(Fs remove_all_f)
} ;;

eval <*D> *Z <D *A bag_fs> *Fs <A> *Elem do_delete @public -> <D> {
  Z Elem &(Fs delete_f)
} ;;

eval <*D> *Z <D *A bag_fs> *Fs <A> *Elem do_delete_all @public -> <D> {
  Z Elem &(Fs delete_all_f)
} ;;

eval <*D> *Z <D *A bag_fs> *Fs do_take @public -> <<A D tuple> maybe> {
  Z &(Fs take_f)
} ;;

eval <*D> *Z <D *A bag_fs> *Fs do_clear @public -> <D> {
  Z &(Fs clear_f)
} ;;

eval <*D> *Z <D *A bag_fs> *Fs <A list> *Elems do_add_list | add_list @public -> <D> {
  Z Fs (Elems list.iter) do_add_iter
} ;;

eval <*D> *Z <D *A bag_fs> *Fs <A iter> *Iter do_add_iter | add_iter @public -> <D> {
  Z
  Iter
  \ <D> *Y <A> *E -> <D> { Y Fs E do_add }
  iter.fold
} ;;

eval <*D> *Z <D *A bag_fs> *Fs <A list> *Elems do_delete_list | delete_list @public -> <D> {
  Z Fs (Elems list.iter) do_delete_iter
} ;;

eval <*D> *Z <D *A bag_fs> *Fs <A iter> *Iter do_delete_iter | delete_iter @public -> <D> {
  Z
  Iter
  \ <D> *Y <A> *E -> <D> { Y Fs E do_delete }
  iter.fold
} ;;

eval <*D> *Z <D *A bag_fs> *Fs <A list> *Elems do_delete_all_list | delete_all_list @public -> <D> {
  Z Fs (Elems list.iter) do_delete_all_iter
} ;;

eval <*D> *Z <D *A bag_fs> *Fs <A iter> *Iter do_delete_all_iter | delete_all_iter @public -> <D> {
  Z
  Iter
  \ <D> *Y <A> *E -> <D> { Y Fs E do_delete_all }
  iter.fold
} ;;

eval <*D> *Z <D *A bag_fs> *Fs do_iter | iter @public -> <A iter> {
  \\ <A iter_res> { Z Fs do_iter_next } iter$
} ;;

eval <*D> *Z <D *A bag_fs> *Fs do_iter_next -> <A iter_res> {
  Z Fs do_take >> case {
  ; (| *Elem, *Z' |) value$ ->
    (| Elem, \\ <A iter_res> { Z' Fs do_iter_next } iter$ |) value$
  ; ** -> none$
  }
} ;;
