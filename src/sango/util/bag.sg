###########################################################################
# MIT License                                                             #
# Copyright (c) 2018 Isao Akiyama                                         #
#                                                                         #
# Permission is hereby granted, free of charge, to any person obtaining   #
# a copy of this software and associated documentation files (the         #
# "Software"), to deal in the Software without restriction, including     #
# without limitation the rights to use, copy, modify, merge, publish,     #
# distribute, sublicense, and/or sell copies of the Software, and to      #
# permit persons to whom the Software is furnished to do so, subject to   #
# the following conditions:                                               #
#                                                                         #
# The above copyright notice and this permission notice shall be          #
# included in all copies or substantial portions of the Software.         #
#                                                                         #
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,         #
# EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF      #
# MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.  #
# IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY    #
# CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,    #
# TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE       #
# SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.                  #
###########################################################################
module "sango.util.bag" ;;

import "sango.list" -> list ;;
import "sango.util.iter" -> iter ;;

### definition of common-behavior object

alias type <*A d> @public := <A _1+> ;;

data <*A bag> @public :=
  d: <A d>
  fs: <<A d> A bag_fs>
  bag$ ;;

data <*A! fxbag> @public :=
  d: <A d>
  fs: <<A d> A bag_fs>
  fxbag$
++ as_a'bag -> [ A a'bag ] ;;

eval <*A! bag> *Bag as_fxbag @public -> <A fxbag> {
  Bag = d: *D fs: *Fs bag$,
  d: D fs: Fs fxbag$
} ;;

eval <*A! fxbag> *FxBag as_bag @public -> <A bag> {
  FxBag = d: *D fs: *Fs fxbag$,
  d: D fs: Fs bag$
} ;;


### definition of function set

data <*D *A bag_fs> @public :=
  empty?_f: <D bool fun>
  size_f: <D int fun>
  contain?_f: <D A bool fun>
  get_all_f: <D A <A list> fun>
  add_f: <D A D fun>
  remove_f: <D A <D <A maybe> tuple> fun>
  remove_all_f: <D A <D <A list> tuple> fun>
  delete_f_: <<D A D fun> maybe>
  delete_all_f_: <<D A D fun> maybe>
  take_f: <D <<A D tuple> maybe> fun>
  clear_f: <D D fun>
  bag_fs$ ;;


### definition of feature

feature <*T!> [ *A a'bag ] @public -> <T A bag_fo> ;;

data <*T! *A bag_fo> @public :=
  d: <T>
  fs: <T A bag_fs>
  bag_fo$
++ as_a'col -> [ A a'col ] ;;


### implementation of common-behavior object

eval <*A bag> *Bag c_empty? | empty? @public -> <bool> {
  Bag = d: *D fs: *Fs bag$,
  D Fs _empty?
} ;;

eval <*A! fxbag> *FxBag fx_empty? | empty? @public -> <bool> {
  (FxBag as_bag) c_empty?
} ;;

eval <*A bag> *Bag c_size | size @public -> <int> {
  Bag = d: *D fs: *Fs bag$,
  D Fs _size
} ;;

eval <*A! fxbag> *FxBag fx_size | size @public -> <int> {
  (FxBag as_bag) c_size
} ;;

eval <*A bag> *Bag <A> *Elem c_contain? | contain? @public -> <bool> {
  Bag = d: *D fs: *Fs bag$,
  D Fs Elem _contain?
} ;;

eval <*A! fxbag> *FxBag <A> *Elem fx_contain? | contain? @public -> <bool> {
  (FxBag as_bag) Elem c_contain?
} ;;

eval <*A bag> *Bag <A> *Elem c_get_all | get_all @public -> <A list> {
  Bag = d: *D fs: *Fs bag$,
  D Fs Elem _get_all
} ;;

eval <*A! fxbag> *FxBag <A> *Elem fx_get_all | get_all @public -> <A list> {
  (FxBag as_bag) Elem c_get_all
} ;;

eval <*A bag> *Bag <A> *Elem c_add | add @public -> <A bag> {
  Bag = d: *D fs: *Fs bag$,
  D Fs Elem _add = *D',
  d: D' fs: Fs bag$
} ;;

eval <*A! fxbag> *FxBag <A> *Elem fx_add | add @public -> <A fxbag> {
  (FxBag as_bag) Elem c_add = *Bag',
  Bag' as_fxbag
} ;;

eval <*A bag> *Bag <A> *Elem c_remove | remove @public -> <<A bag> <A maybe> tuple> {
  Bag = d: *D fs: *Fs bag$,
  D Fs Elem _remove = (| *D', *Removed_ |),
  (| d: D' fs: Fs bag$, Removed_ |)
} ;;

eval <*A! fxbag> *FxBag <A> *Elem fx_remove | remove @public -> <<A fxbag> <A maybe> tuple> {
  (FxBag as_bag) Elem c_remove = (| *Bag', *Removed_ |),
  (| Bag' as_fxbag, Removed_ |)
} ;;

eval <*A bag> *Bag <A> *Elem c_remove_all | remove_all @public -> <<A bag> <A list> tuple> {
  Bag = d: *D fs: *Fs bag$,
  D Fs Elem _remove_all = (| *D', *Removed_list |),
  (| d: D' fs: Fs bag$, Removed_list |)
} ;;

eval <*A! fxbag> *FxBag <A> *Elem fx_remove_all | remove_all @public -> <<A fxbag> <A list> tuple> {
  (FxBag as_bag) Elem c_remove_all = (| *Bag', *Removed_list |),
  (| Bag' as_fxbag, Removed_list |)
} ;;

eval <*A bag> *Bag <A> *Elem c_delete | delete @public -> <A bag> {
  Bag = d: *D fs: *Fs bag$,
  D Fs Elem _delete = *D',
  d: D' fs: Fs bag$
} ;;

eval <*A! fxbag> *FxBag <A> *Elem fx_delete | delete @public -> <A fxbag> {
  (FxBag as_bag) Elem c_delete = *Bag',
  Bag' as_fxbag
} ;;

eval <*A bag> *Bag <A> *Elem c_delete_all | delete_all @public -> <A bag> {
  Bag = d: *D fs: *Fs bag$,
  D Fs Elem _delete_all = *D',
  d: D' fs: Fs bag$
} ;;

eval <*A! fxbag> *FxBag <A> *Elem fx_delete_all | delete_all @public -> <A fxbag> {
  (FxBag as_bag) Elem c_delete_all = *Bag',
  Bag' as_fxbag
} ;;

eval <*A bag> *Bag c_take | take @public -> <<A <A bag> tuple> maybe> {
  Bag = d: *D fs: *Fs bag$,
  D Fs _take >> case {
  ; (| *Elem, *D' |) value$ -> (| Elem, d: D' fs: Fs bag$ |) value$
  ; ** -> none$
  }
} ;;

eval <*A! fxbag> *FxBag fx_take | take @public -> <<A <A fxbag> tuple> maybe> {
  (FxBag as_bag) c_take >> case {
  ; (| *Elem, *Bag' |) value$ -> (| Elem, Bag' as_fxbag |) value$
  ; ** -> none$
  }
} ;;

eval <*A bag> *Bag c_clear | clear @public -> <A bag> {
  Bag = d: *D fs: *Fs bag$,
  D Fs _clear = *D',
  d: D' fs: Fs bag$
} ;;

eval <*A! fxbag> *FxBag fx_clear | clear @public -> <A fxbag> {
  (FxBag as_bag) c_clear = *Bag',
  Bag' as_fxbag
} ;;

eval <*A bag> *Bag <A list> *Elems c_add_list | add_list @public -> <A bag> {
  Bag (Elems list.iter) c_add_iter
} ;;

eval <*A bag> *Bag <A iter> *Iter c_add_iter | add_iter @public -> <A bag> {
  Iter next >> case {
  ; (| *Elem, *Iter' |) value$ -> (Bag Elem add) Iter' &&
  ; ** -> Bag
  }
} ;;

eval <*A! fxbag> *FxBag <A list> *Elems fx_add_list | add_list @public -> <A fxbag> {
  (FxBag as_bag) Elems c_add_list = *Bag',
  Bag' as_fxbag
} ;;

eval <*A! fxbag> *FxBag <A iter> *Iter fx_add_iter | add_iter @public -> <A fxbag> {
  (FxBag as_bag) Iter c_add_iter = *Bag',
  Bag' as_fxbag
} ;;

eval <*A bag> *Bag <A list> *Elems c_delete_list | delete_list @public -> <A bag> {
  Bag (Elems list.iter) c_delete_iter
} ;;

eval <*A bag> *Bag <A iter> *Iter c_delete_iter | delete_iter @public -> <A bag> {
  Iter next >> case {
  ; (| *Elem, *Iter' |) value$ -> (Bag Elem c_delete) Iter' &&
  ; ** -> Bag
  }
} ;;

eval <*A! fxbag> *FxBag <A list> *Elems fx_delete_list | delete_list @public -> <A fxbag> {
  (FxBag as_bag) Elems c_delete_list = *Bag',
  Bag' as_fxbag
} ;;

eval <*A! fxbag> *FxBag <A iter> *Iter fx_delete_iter | delete_iter @public -> <A fxbag> {
  (FxBag as_bag) Iter c_delete_iter = *Bag',
  Bag' as_fxbag
} ;;

eval <*A bag> *Bag <A list> *Elems c_delete_all_list | delete_all_list @public -> <A bag> {
  Bag (Elems list.iter) c_delete_all_iter
} ;;

eval <*A bag> *Bag <A iter> *Iter c_delete_all_iter | delete_all_iter @public -> <A bag> {
  Iter next >> case {
  ; (| *Elem, *Iter' |) value$ -> (Bag Elem c_delete_all) Iter' &&
  ; ** -> Bag
  }
} ;;

eval <*A! fxbag> *FxBag <A list> *Elems fx_delete_all_list | delete_all_list @public -> <A fxbag> {
  (FxBag as_bag) Elems c_delete_all_list = *Bag',
  Bag' as_fxbag
} ;;

eval <*A! fxbag> *FxBag <A iter> *Iter fx_delete_all_iter | delete_all_iter @public -> <A fxbag> {
  (FxBag as_bag) Iter c_delete_all_iter = *Bag',
  Bag' as_fxbag
} ;;

eval <*A bag> *Bag c_iter | iter @public -> <A iter> {
  \\ <A iter_res> { Bag c_iter_next } iter$
} ;;

eval <*A bag> *Bag c_iter_next -> <A iter_res> {
  Bag c_take >> case {
  ; (| *Elem, *Bag' |) value$ ->
    (| Elem, \\ <A iter_res> { Bag' c_iter_next } iter$ |) value$
  ; ** -> none$
  }
} ;;

eval <*A! fxbag> *FxBag fx_iter | iter @public -> <A iter> {
  (FxBag as_bag) c_iter
} ;;


### framework of faeture

eval <*T![ *A a'bag ]> *FBag f_empty? | empty? @public -> <bool> {
  FBag _feature_a'bag = *Bag_fo,
  Bag_fo fo_empty?
} ;;

eval <*T! *A bag_fo> *Bag_fo fo_empty? | empty? @public -> <bool> {
  Bag_fo = d: *D fs: *Fs bag_fo$,
  D Fs _empty?
} ;;

eval <*T![ *A a'bag ]> *FBag f_size | size @public -> <int> {
  FBag _feature_a'bag = *Bag_fo,
  Bag_fo fo_size
} ;;

eval <*T! *A bag_fo> *Bag_fo fo_size | size @public -> <int> {
  Bag_fo = d: *D fs: *Fs bag_fo$,
  D Fs _size
} ;;

eval <*T![ *A a'bag ]> *FBag <A> *Elem f_contain? | contain? @public -> <bool> {
  FBag _feature_a'bag = *Bag_fo,
  Bag_fo Elem fo_contain?
} ;;

eval <*T! *A bag_fo> *Bag_fo <A> *Elem fo_contain? | contain? @public -> <bool> {
  Bag_fo = d: *D fs: *Fs bag_fo$,
  D Fs Elem _contain?
} ;;

eval <*T![ *A a'bag ]> *FBag <A> *Elem f_get_all | get_all @public -> <A list> {
  FBag _feature_a'bag = *Bag_fo,
  Bag_fo Elem fo_get_all
} ;;

eval <*T! *A bag_fo> *Bag_fo <A> *Elem fo_get_all | get @public -> <A list> {
  Bag_fo = d: *D fs: *Fs bag_fo$,
  D Fs Elem _get_all
} ;;

eval <*T![ *A a'bag ]> *FBag <A> *Elem f_add | add @public -> <T> {
  FBag _feature_a'bag = *Bag_fo,
  Bag_fo Elem fo_add = *Bag_fo',
  Bag_fo' = d: *D' fs: *Fs' bag_fo$,
  D'
} ;;

eval <*T! *A bag_fo> *Bag_fo <A> *Elem fo_add | add @public -> <T A bag_fo> {
  Bag_fo = d: *D fs: *Fs bag_fo$,
  D Fs Elem _add = *D',
  d: D' fs: Fs bag_fo$
} ;;

eval <*T![ *A a'bag ]> *FBag <A> *Elem f_remove | remove @public -> <T <A maybe> tuple> {
  FBag _feature_a'bag = *Bag_fo,
  Bag_fo Elem fo_remove = (| *Bag_fo', *Removed_ |),
  Bag_fo' = d: *D' fs: *Fs' bag_fo$,
  (| D', Removed_ |)
} ;;

eval <*T! *A bag_fo> *Bag_fo <A> *Elem fo_remove | remove @public -> <<T A bag_fo> <A maybe> tuple> {
  Bag_fo = d: *D fs: *Fs bag_fo$,
  D Fs Elem _remove = (| *D', *Removed_ |),
  (| d: D' fs: Fs bag_fo$, Removed_ |)
} ;;

eval <*T![ *A a'bag ]> *FBag <A> *Elem f_remove_all | remove_all @public -> <T <A list> tuple> {
  FBag _feature_a'bag = *Bag_fo,
  Bag_fo Elem fo_remove_all = (| *Bag_fo', *Removed_list |),
  Bag_fo' = d: *D' fs: *Fs' bag_fo$,
  (| D', Removed_list |)
} ;;

eval <*T! *A bag_fo> *Bag_fo <A> *Elem fo_remove_all | remove_all @public -> <<T A bag_fo> <A list> tuple> {
  Bag_fo = d: *D fs: *Fs bag_fo$,
  D Fs Elem _remove_all = (| *D', *Removed_list |),
  (| d: D' fs: Fs bag_fo$, Removed_list |)
} ;;

eval <*T![ *A a'bag ]> *FBag <A> *Elem f_delete | delete @public -> <T> {
  FBag _feature_a'bag = *Bag_fo,
  Bag_fo Elem fo_delete = *Bag_fo',
  Bag_fo' = d: *D' fs: *Fs' bag_fo$,
  D'
} ;;

eval <*T! *A bag_fo> *Bag_fo <A> *Elem fo_delete | delete @public -> <T A bag_fo> {
  Bag_fo = d: *D fs: *Fs bag_fo$,
  D Fs Elem _delete = *D',
  d: D' fs: Fs bag_fo$
} ;;

eval <*T![ *A a'bag ]> *FBag <A> *Elem f_delete_all | delete_all @public -> <T> {
  FBag _feature_a'bag = *Bag_fo,
  Bag_fo Elem fo_delete_all = *Bag_fo',
  Bag_fo' = d: *D' fs: *Fs' bag_fo$,
  D'
} ;;

eval <*T! *A bag_fo> *Bag_fo <A> *Elem fo_delete_all | delete_all @public -> <T A bag_fo> {
  Bag_fo = d: *D fs: *Fs bag_fo$,
  D Fs Elem _delete_all = *D',
  d: D' fs: Fs bag_fo$
} ;;

eval <*T![ *A a'bag ]> *FBag f_take | take @public -> <<A T tuple> maybe> {
  FBag _feature_a'bag = *Bag_fo,
  Bag_fo fo_take >> case {
  ; (| *Elem, d: *D' fs: *Fs' bag_fo$ |) value$ -> (| Elem, D' |) value$
  ; ** -> none$
  }
} ;;

eval <*T! *A bag_fo> *Bag_fo fo_take | take @public -> <<A <T A bag_fo> tuple> maybe> {
  Bag_fo = d: *D fs: *Fs bag_fo$,
  D Fs _take >> case {
  ; (| *Elem, *D' |) value$ -> (| Elem, d: D' fs: Fs bag_fo$ |) value$
  ; ** -> none$
  }
} ;;

eval <*T![ *A a'bag ]> *FBag f_clear | clear @public -> <T> {
  FBag _feature_a'bag = *Bag_fo,
  Bag_fo fo_clear = *Bag_fo',
  Bag_fo' = d: *D' fs: *Fs' bag_fo$,
  D'
} ;;

eval <*T! *A bag_fo> *Bag_fo fo_clear | clear @public -> <T A bag_fo> {
  Bag_fo = d: *D fs: *Fs bag_fo$,
  D Fs _clear = *D',
  d: D' fs: Fs bag_fo$
} ;;

eval <*T![ *A a'bag ]> *FBag <A list> *Elems f_add_list | add_list @public -> <T> {
  FBag (Elems list.iter) f_add_iter
} ;;

eval <*T! *A bag_fo> *Bag_fo <A list> *Elems fo_add_list | add_list @public -> <T A bag_fo> {
  Bag_fo (Elems list.iter) fo_add_iter
} ;;

eval <*T![ *A a'bag ]> *FBag <A iter> *Iter f_add_iter | add_iter @public -> <T> {
  FBag _feature_a'bag = *Bag_fo,
  Bag_fo Iter fo_add_iter = *Bag_fo',
  Bag_fo' = d: *D' fs: *Fs' bag_fo$,
  D'
} ;;

eval <*T! *A bag_fo> *Bag_fo <A iter> *Iter fo_add_iter | add_iter @public -> <T A bag_fo> {
  Iter next >> case {
  ; (| *Elem, *Iter' |) value$ -> (Bag_fo Elem fo_add) Iter' &&
  ; ** -> Bag_fo
  }
} ;;

eval <*T![ *A a'bag ]> *FBag <A list> *Elems f_delete_list | delete_list @public -> <T> {
  FBag (Elems list.iter) f_delete_iter
} ;;

eval <*T! *A bag_fo> *Bag_fo <A list> *Elems fo_delete_list | delete_list @public -> <T A bag_fo> {
  Bag_fo (Elems list.iter) fo_delete_iter
} ;;

eval <*T![ *A a'bag ]> *FBag <A iter> *Iter f_delete_iter | delete_iter @public -> <T> {
  FBag _feature_a'bag = *Bag_fo,
  Bag_fo Iter fo_delete_iter = *Bag_fo',
  Bag_fo' = d: *D' fs: *Fs' bag_fo$,
  D'
} ;;

eval <*T! *A bag_fo> *Bag_fo <A iter> *Iter fo_delete_iter | delete_iter @public -> <T A bag_fo> {
  Iter next >> case {
  ; (| *Elem, *Iter' |) value$ -> (Bag_fo Elem fo_delete) Iter' &&
  ; ** -> Bag_fo
  }
} ;;

eval <*T![ *A a'bag ]> *FBag <A list> *Elems f_delete_all_list | delete_all_list @public -> <T> {
  FBag (Elems list.iter) f_delete_all_iter
} ;;

eval <*T![ *A a'bag ]> *FBag <A iter> *Iter f_delete_all_iter | delete_all_iter @public -> <T> {
  FBag _feature_a'bag = *Bag_fo,
  Bag_fo Iter fo_delete_all_iter = *Bag_fo',
  Bag_fo' = d: *D' fs: *Fs' bag_fo$,
  D'
} ;;

eval <*T! *A bag_fo> *Bag_fo <A iter> *Iter fo_delete_all_iter | delete_all_iter @public -> <T A bag_fo> {
  Iter next >> case {
  ; (| *Elem, *Iter' |) value$ -> (Bag_fo Elem fo_delete_all) Iter' &&
  ; ** -> Bag_fo
  }
} ;;

eval <*T![ *A a'bag ]> *FBag f_iter | iter @public -> <A iter> {
  \\ <A iter_res> { FBag f_iter_next } iter$
} ;;

eval <*T![ *A a'bag ]> *FBag f_iter_next -> <A iter_res> {
  FBag f_take >> case {
  ; (| *Elem, *FBag' |) value$ ->
    (| Elem, \\ <A iter_res> { FBag' f_iter_next } iter$ |) value$
  ; ** -> none$
  }
} ;;

eval <*T! *A bag_fo> *Bag_fo fo_iter | iter @public -> <A iter> {
  \\ <A iter_res> { Bag_fo fo_iter_next } iter$
} ;;

eval <*T! *A bag_fo> *Bag_fo fo_iter_next -> <A iter_res> {
  Bag_fo fo_take >> case {
  ; (| *Elem, *Bag_fo' |) value$ ->
    (| Elem, \\ <A iter_res> { Bag_fo' fo_iter_next } iter$ |) value$
  ; ** -> none$
  }
} ;;


### feature implementation of feature object

eval <*T! *A bag_fo> *Bag_fo as_a'col -> <A col> {
  Bag_fo [] &\ <T A bag_fo> *Fo <A list> *EsR -> <A list> {
    Fo fo_take >> case {
    ; (| *E, *Fo' |) value$ -> Fo' [ E ; EsR ] &&
    ; ** -> EsR list.reverse
    }
  } = *Es,
  d: (Es fo_col_d$) fs: bag_fo_col_fs col$
} ;;

extend <*A _1> -> fo_col_d := <A list> fo_col_d$ ;;

eval bag_fo_col_fs -> <<*A col_d> A col_fs> {
  empty?_f_: (\ <A col_d> *D -> <bool> {
    D = *Es fo_col_d$, Es list.empty?
  } value$)
  size_f: \ <A col_d> *D -> <int> {
    D = *Es fo_col_d$, Es list.length
  }
  take_f: \ <A col_d> *D
      -> <<A <A col_d> tuple> maybe> {
    D = *Es fo_col_d$, Es case {
    ; [ *E ; *Es' ] -> (| E, Es' fo_col_d$ |) value$
    ; ** -> none$
    }
  }
  col_fs$
} ;;


### feature implementation of common-behavior object

eval <*A! fxbag> *FxBag as_a'bag -> <<A fxbag> A bag_fo> {
  d: FxBag fs: fxbag_fo_fs bag_fo$
} ;;

eval fxbag_fo_fs -> <<*A! fxbag> A bag_fs> {
  empty?_f: ^fx_empty?
  size_f: ^fx_size
  contain?_f: ^fx_contain?
  get_all_f: ^fx_get_all
  add_f: ^fx_add
  remove_f: ^fx_remove
  remove_all_f: ^fx_remove_all
  delete_f_: (^fx_delete value$)
  delete_all_f_: (^fx_delete_all value$)
  take_f: ^fx_take
  clear_f: ^fx_clear
  bag_fs$
} ;;


### subroutines

eval <*D *A bag_fs> *Fs bag_fs_delete_f | delete_f @public -> <D A D fun> {
  Fs delete_f_ >> case {
  ; *F value$ -> F
  ; none$ ->
    \ <D> *DD <A> *Elem -> <D> {
      DD Elem &(Fs remove_f) = (| *DD', ** |),
      DD'
    }
  }
} ;;

eval <*D *A bag_fs> *Fs bag_fs_delete_all_f | delete_all_f @public -> <D A D fun> {
  Fs delete_all_f_ >> case {
  ; *F value$ -> F
  ; none$ ->
    \ <D> *DD <A> *Elem -> <D> {
      DD Elem &(Fs remove_all_f) = (| *DD', ** |),
      DD'
    }
  }
} ;;

eval <*D> *DD <D *A bag_fs> *Fs _empty? -> <bool> {
  DD &(Fs empty?_f)
} ;;

eval <*D> *DD <D *A bag_fs> *Fs _size -> <int> {
  DD &(Fs size_f)
} ;;

eval <*D> *DD <D *A bag_fs> *Fs <A> *Elem _contain? -> <bool> {
  DD Elem &(Fs contain?_f)
} ;;

eval <*D> *DD <D *A bag_fs> *Fs <A> *Elem _get_all -> <A list> {
  DD Elem &(Fs get_all_f)
} ;;

eval <*D> *DD <D *A bag_fs> *Fs <A> *Elem _add -> <D> {
  DD Elem &(Fs add_f)
} ;;

eval <*D> *DD <D *A bag_fs> *Fs <A> *Elem _remove -> <D <A maybe> tuple> {
  DD Elem &(Fs remove_f)
} ;;

eval <*D> *DD <D *A bag_fs> *Fs <A> *Elem _remove_all -> <D <A list> tuple> {
  DD Elem &(Fs remove_all_f)
} ;;

eval <*D> *DD <D *A bag_fs> *Fs <A> *Elem _delete -> <D> {
  DD Elem &(Fs delete_f)
} ;;

eval <*D> *DD <D *A bag_fs> *Fs <A> *Elem _delete_all -> <D> {
  DD Elem &(Fs delete_all_f)
} ;;

eval <*D> *DD <D *A bag_fs> *Fs _take -> <<A D tuple> maybe> {
  DD &(Fs take_f)
} ;;

eval <*D> *DD <D *A bag_fs> *Fs _clear -> <D> {
  DD &(Fs clear_f)
} ;;
