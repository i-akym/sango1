###########################################################################
# MIT License                                                             #
# Copyright (c) 2024 AKIYAMA Isao                                         #
#                                                                         #
# Permission is hereby granted, free of charge, to any person obtaining   #
# a copy of this software and associated documentation files (the         #
# "Software"), to deal in the Software without restriction, including     #
# without limitation the rights to use, copy, modify, merge, publish,     #
# distribute, sublicense, and/or sell copies of the Software, and to      #
# permit persons to whom the Software is furnished to do so, subject to   #
# the following conditions:                                               #
#                                                                         #
# The above copyright notice and this permission notice shall be          #
# included in all copies or substantial portions of the Software.         #
#                                                                         #
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,         #
# EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF      #
# MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.  #
# IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY    #
# CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,    #
# TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE       #
# SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.                  #
###########################################################################
module @beta "sango.util.map.shared.box" ;;

import "sango.actor.util" -> actu ;;
import "sango.entity.box" -> box ;;
import "sango.list" -> list ;;
import "sango.util.map" -> map ;;
import "sango.util.map.shared" -> shmap ;;

eval <*A! *B! map.map> *Map create_box @public -> <A B shmap.shared_map_h> {
  Map none$ box.create_box = *Box,
  Box actu.create_box_monitor = *Mon,

  extract_f: \\ <<A B tuple> iter> { Mon extract }
  empty?_f: \\ <bool> { Mon empty? }
  size_f: \\ <int> { Mon size }
  get_f: \ <A> *Key -> <<A B tuple> maybe> { Mon Key get }
  assoc_f_: (\ <A> *Key -> <B maybe> { Mon Key assoc } value$)
  contain_key?_f_: (\ <A> *Key -> <bool> { Mon Key contain_key? } value$)
  put_f: \ <A> *Key <B> *Assoc -> <<A B tuple> maybe> { Mon Key Assoc put }
  put_new_f_: (\ <A> *Key <B> *Assoc -> <bool> { Mon Key Assoc put_new } value$)
  add_f_: (\ <A> *Key <B> *Assoc -> <void> { Mon Key Assoc add } value$)
  remove_f: \ <A> *Key -> <<A B tuple> maybe> { Mon Key remove }
  delete_f_: (\ <A> *Key -> <void> { Mon Key delete } value$)
  take_f: \\ <<A B tuple> maybe> { Mon take }
  clear_f: \\ <void> { Mon clear }
  shmap.shared_map_h$
} ;;

eval <<*A! *B! map.map> actu.box_monitor_h> *Mon extract -> <<A B tuple> iter> {
  Mon
    \ <<A B map.map> box.box_h> *Box -> <A B map.map> {
      Box box.read
    } actu.run = *Z,
  Z map.iter
} ;;

eval <<*A! *B! map.map> actu.box_monitor_h> *Mon empty? -> <bool> {
  Mon
    \ <<A B map.map> box.box_h> *Box -> <A B map.map> {
      Box box.read
    } actu.run = *Z,
  Z map.empty?
} ;;

eval <<*A! *B! map.map> actu.box_monitor_h> *Mon size -> <int> {
  Mon
    \ <<A B map.map> box.box_h> *Box -> <A B map.map> {
      Box box.read
    } actu.run = *Z,
  Z map.size
} ;;

eval <<*A! *B! map.map> actu.box_monitor_h> *Mon <A> *Key get -> <<A B tuple> maybe> {
  Mon
    \ <<A B map.map> box.box_h> *Box -> <A B map.map> {
      Box box.read
    } actu.run = *Z,
  Z Key map.get
} ;;

eval <<*A! *B! map.map> actu.box_monitor_h> *Mon <A> *Key assoc -> <B maybe> {
  Mon
    \ <<A B map.map> box.box_h> *Box -> <A B map.map> {
      Box box.read
    } actu.run = *Z,
  Z Key map.assoc
} ;;

eval <<*A! *B! map.map> actu.box_monitor_h> *Mon <A> *Key contain_key? -> <bool> {
  Mon
    \ <<A B map.map> box.box_h> *Box -> <A B map.map> {
      Box box.read
    } actu.run = *Z,
  Z Key map.contain_key?
} ;;

eval <<*A! *B! map.map> actu.box_monitor_h> *Mon <A> *Key <B> *Assoc put -> <<A B tuple> maybe> {
  Mon
    \ <<A B map.map> box.box_h> *Box -> <<A B tuple> maybe> {
      Box box.read = *Z,
      Z Key Assoc map.put = (| *Z', *Replaced_kv_ |),
      Box Z' box.write,
      Replaced_kv_
    } actu.run
} ;;

eval <<*A! *B! map.map> actu.box_monitor_h> *Mon <A> *Key <B> *Assoc put_new -> <bool> {
  Mon
    \ <<A B map.map> box.box_h> *Box -> <bool> {
      Box box.read = *Z,
      Z Key Assoc map.put_new = (| *Z', *Succeeded? |),
      Box Z' box.write,
      Succeeded?
    } actu.run
} ;;

eval <<*A! *B! map.map> actu.box_monitor_h> *Mon <A> *Key <B> *Assoc add -> <void> {
  Mon
    \ <<A B map.map> box.box_h> *Box -> <void> {
      Box box.read = *Z,
      Z Key Assoc map.add = *Z',
      Box Z' box.write >> ignore
    } actu.run
} ;;

eval <<*A! *B! map.map> actu.box_monitor_h> *Mon <A> *Key remove -> <<A B tuple> maybe> {
  Mon
    \ <<A B map.map> box.box_h> *Box -> <<A B tuple> maybe> {
      Box box.read = *Z,
      Z Key map.remove = (| *Z', *Removed_kv_ |),
      Box Z' box.write,
      Removed_kv_
    } actu.run
} ;;

eval <<*A! *B! map.map> actu.box_monitor_h> *Mon <A> *Key delete -> <void> {
  Mon
    \ <<A B map.map> box.box_h> *Box -> <void> {
      Box box.read = *Z,
      Z Key map.delete = *Z',
      Box Z' box.write >> ignore
    } actu.run
} ;;

eval <<*A! *B! map.map> actu.box_monitor_h> *Mon take -> <<A B tuple> maybe> {
  Mon
    \ <<A B map.map> box.box_h> *Box -> <<A B tuple> maybe> {
      Box box.read = *Z,
      Z map.take >> case {
      ; (| *KV, *Z' |) value$ -> Box Z' box.write, KV value$
      ; ** -> none$
      }
    } actu.run
} ;;

eval <<*A! *B! map.map> actu.box_monitor_h> *Mon clear -> <void> {
  Mon
    \ <<A B map.map> box.box_h> *Box -> <void> {
      Box box.read = *Z,
      Z map.clear = *Z',
      Box Z' box.write >> ignore
    } actu.run
} ;;
